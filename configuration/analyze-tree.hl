
/*
 * Contains our Hyperlambda analyze tree.
 */





/*
 * Analyzing keywords in your code.
 */
analyse-keywords:Discuss keywords
  action-name:Analyse keywords
  .is-report
  .evaluate

    /*
     * Extracting lambda from session, figuring out how many references to different
     * events the lambda contains, and giving user feedback about our findings.
     */
    _no:int:0
    .events
    p5.web.session.get:hypereval.analyze-lambda
    vocabulary
    set:x:/@vocabulary/*/=~.
    for-each:x:/@vocabulary/*?value

      /*
       * Checking if Active Event exists in lambda, and if so, bumping number of events.
       */
      if:x:/@p5.web.session.get/*/*/**/\{0}
        :x:/@_dp?value

        /*
         * Currently iterated Active Event was found in lambda from session.
         */
        set:x:/@_no?value
          +:x:/@_no?value
            _:1
        add:x:/@.events
          src:<strong>[{0}]</strong>
            :x:/@_dp?value

    /*
     * Providing visual feedback about which events we found.
     */
    join:x:/@.events/*?name
      sep:", "
    hypereval.text-feedback:We found the following keywords in your code {0}
      :x:/@join?value

    /*
     * Now we know how many events the code contains.
     */
    micro.speak:Your code contains {0} distinct keywords.
      :x:/@_no?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing events in your code.
 */
analyse-events:Discuss events
  action-name:Analyse keywords
  .is-report
  .evaluate

    /*
     * Extracting lambda from session, figuring out how many references to different
     * events the lambda contains, and giving user feedback about our findings.
     */
    _no:int:0
    .events
    p5.web.session.get:hypereval.analyze-lambda
    vocabulary
    for-each:x:/@vocabulary/*?value

      /*
       * Checking if Active Event exists in lambda, and if so, bumping number of events.
       */
      if:x:/@p5.web.session.get/*/*/**/\{0}
        :x:/@_dp?value

        /*
         * Currently iterated Active Event was found in lambda from session.
         */
        set:x:/@_no?value
          +:x:/@_no?value
            _:1
        add:x:/@.events
          src:<strong>[{0}]</strong>
            :x:/@_dp?value

    /*
     * Providing visual feedback about which events we found.
     */
    join:x:/@.events/*?name
      sep:", "
    hypereval.text-feedback:We found the following events in your code {0}
      :x:/@join?value

    /*
     * Now we know how many events the code contains.
     */
    micro.speak:Your code contains {0} Active Events.
      :x:/@_no?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing nodes in your code.
 */
analyse-nodes:Is it dry
  action-name:Analyse DRYness of code
  .is-report
  .evaluate

    /*
     * Extracting number of nodes, and number of unique nodes in lambda.
     */
    p5.web.session.get:hypereval.analyze-lambda

    /*
     * Providing visual feedback about results.
     */
    hypereval.text-feedback:There are {0} distinct nodes in your code.
      :x:/@p5.web.session.get/*/*/**/$?count

    /*
     * Figuring out ratio of unique nodes to nodes in total.
     */
    eval-x:x:/+|/+2
    _nodes:x:/@p5.web.session.get/*/*/**?count
    _unique-nodes:x:/@p5.web.session.get/*/*/**/$?count
    -:x:/@_nodes?value
      _:x:/@_unique-nodes?value
    -:x:/@_unique-nodes?value
      _:x:/./-?value
    /:x:/@-?value.decimal
      _:x:/@_nodes?value.decimal
    *:x:/-?value
      _:int:100
    _extra
    if:x:/@*?value.int
      >:int:80
      set:x:/@_extra?value
        src:" Your code is very dry, and does not repeat itself much at all, which is a good thing."
    else-if:x:/@*?value.int
      >:int:50
      set:x:/@_extra?value
        src:" Your code is fairly dry, and does not repeat itself too much, this is OK."
    else-if:x:/@*?value.int
      >:int:20
      set:x:/@_extra?value
        src:" Your code is not dry, and does repeat itself too much. You should probably break it up more, by using loops, expressions, or by creating more supporting Active Events."

    /*
     * Now we know how many events the code contains.
     */
    micro.speak:You have {0} distinct nodes in your code, {1} nodes in total, and {2} percent of your nodes are unique. {3}
      :x:/@p5.web.session.get/*/*/**/$?count
      :x:/@p5.web.session.get/*/*/**?count
      :x:/@*?value.int
      :x:/@_extra?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing comments in your code.
 */
analyse-commenting:What about comments
  action-name:Analyse comments
  .is-report
  .evaluate

    /*
     * Extracting code and lambda from session, figuring out the ratio of comments
     * to nodes, and providing feedback to user.
     */
    p5.web.session.get:hypereval.analyze-lambda
    lambda2hyper:x:/@p5.web.session.get/*/*/*
    p5.web.session.get:hypereval.analyze-code

    /*
     * Figuring out ratio of comments to code.
     */
    length:x:/@lambda2hyper?value
    length:x:/@p5.web.session.get/*?value
    /:x:/../*/length/[0,1]?value.decimal
      _:x:/../*/length/[1,2]?value.decimal
    *:x:/-?value
      _:decimal:100
    -:int:100
      _:x:/@*?value.int
    _extra
    if:x:/@-?value.int
      >:int:80
      set:x:/@_extra?value
        src:" and have commented your code very well."
    else-if:x:/@-?value.int
      >:int:50
      set:x:/@_extra?value
        src:" and have commented your code fairly well."
    else-if:x:/@-?value.int
      >:int:20
      set:x:/@_extra?value
        src:" and should comment your code more."
    else
      set:x:/@_extra?value
        src:" which is not good, in fact it is quite terrible, and makes your code more difficult to understand."


    /*
     * Providing visual feedback about which events we found.
     */
    hypereval.text-feedback:{0} percent is comments.
      :x:/@-?value.int

    /*
     * Now we know how many events the code contains.
     */
    micro.speak:{0} percent of your code is comments, {1}
      :x:/@-?value.int
      :x:/@_extra?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing concepts in your code.
 */
analyse-concepts:Discuss concepts
  action-name:Analyse concepts
  .is-report
  .evaluate

    /*
     * Extracting lambda from session, figuring out which different (known) concepts are
     * found in the code.
     */
    _extra
    p5.web.session.get:hypereval.analyze-lambda

    /*
     * List of (known) concepts.
     */
    .concepts
      concept
        text:" Your code is accessing your my sequel database."
        keywords
          ~p5.mysql.
      concept
        text:" Your code is creating widgets."
        keywords
          create-widget
          create-void-widget
          create-literal-widget
          create-container-widget
          p5.web.widgets.create
          p5.web.widgets.create-void
          p5.web.widgets.create-literal
          p5.web.widgets.create-container
          create-widgets
      concept
        text:" Your code is accessing your session collection."
        keywords
          ~p5.web.session
      concept
        text:" Your code is accessing your view state collection."
        keywords
          ~p5.web.viewstate
      concept
        text:" Your code is accessing your cookie collection, which means it seems like it is dependent upon state, across multiple http requests."
        keywords
          ~p5.web.cookie
      concept
        text:" Your code is accessing your http post parameter collection."
        keywords
          ~p5.web.post.
      concept
        text:" Your code is accessing the raw http request, which implies it can possibly be invoked by clients that are not browsers."
        keywords
          p5.web.request.get-method
          p5.web.request.get-body
          p5.web.request.parse-mime
          p5.web.request.save-body
          ~p5.web.header
      concept
        text:" Your code seems to be echoing its own response back to the client, which implies it's some sort of web service, and can be invoked by clients that are not browsers."
        keywords
          p5.web.echo
      concept
        text:" Your code seems to be echoing a file back to the client. Be on the lookout here for not accidentally echoing files back to the client, which allows a malicious adversary to gain access to information he shouldn't gain access to."
        keywords
          p5.web.echo-file
      concept
        text:" Your code seems to be using inline CSS styles. You might want to consider using CSS classes instead, which is arguably better, since it separates representation from content."
        keywords
          create-widget/**/style
          create-void-widget/**/style
          create-literal-widget/**/style
          create-container-widget/**/style
          p5.web.widgets.create/**/style
          p5.web.widgets.create-void/**/style
          p5.web.widgets.create-literal/**/style
          p5.web.widgets.create-container/**/style
          create-widgets/**/style
      concept
        text:" Your code seems to be handling Ajax events."
        keywords
          create-widget/**/~on
          create-void-widget/**/~on
          create-literal-widget/**/~on
          create-container-widget/**/~on
          p5.web.widgets.create/**/~on
          p5.web.widgets.create-void/**/~on
          p5.web.widgets.create-literal/**/~on
          p5.web.widgets.create-container/**/~on
          create-widgets/**/style
      concept
        text:" Your code seems to be using extension widgets, please make sure that these extension widgets actually exists in the environment where you intend to evaluate this code."
        keywords
          ~.widgets.
      concept
        text:" Your code seems to be creating events, which modifies the state of your server. Be on alert here, such that you make sure your events have a unique name space, such that they don't clash with other pre existing events."
        keywords
          create-event
          p5.events.create
      concept
        text:" Your code seems to be deleting events, which modifies the state of your server. Be on alert here, such that you don't accidentally delete events you are dependent upon here."
        keywords
          delete-event
          p5.events.delete
      concept
        text:" Your code is accessing your phosphorus five memory based database."
        keywords
          select-data
          insert-data
          append-data
          delete-data
          p5.data.select
          p5.data.insert
          p5.data.append
          p5.data.delete
      concept
        text:" Your code is dependent upon Micro."
        keywords
          ~micro.
      concept
        text:" Your code is including CSS files, please make sure these files actually exists."
        keywords
          p5.web.include-css-file
      concept
        text:" Your code is including JavaScript files, please make sure these files actually exists, and that they have no syntactis errors."
        keywords
          p5.web.include-javascript-file
      concept
        text:" Your code is returning inline JavaScript from your server, make sure there are no syntactic errors in your JavaScript by testing it thoroughly."
        keywords
          p5.web.send-javascript
      concept
        text:" Your code is evaluating code, which implies you should be carefully looking for unintentional side-effects, such as evaluating malicious code by accident."
        keywords
          eval
          eval-mutable
      concept
        text:" Your code is evaluating code, but since it does this using a whitelist, this is probably OK."
        keywords
          eval-whitelist
      concept
        text:" Your code is evaluating Hyperlambda files, make sure that these files exists, and that there are no syntactic errors in them."
        keywords
          micro.evaluate.file
      concept
        text:" Your code is evaluating and entire Hyperlambda folder, which implies that it will evaluate every single Hyperlambda file, recursively found inside of the specified folder. Please make sure it's impossible to inject malicious code into this folder."
        keywords
          micro.evaluate.folder
      concept
        text:" Your code is accessing the file system directly, make sure you don't accidentally delete files you are dependent upon, or access files that doesn't exist."
        keywords
          load-file
          save-file
          delete-file
          move-file
          copy-file
          file-exists
          ~p5.io.
      concept
        text:" Your code is creating a PayPal button, may you be rich and have everything your heart desires."
        keywords
          micro.widgets.paypal-button
      concept
        text:" Your code is consuming the cryptographic services of phosphorus five."
        keywords
          ~p5.crypto.
      concept
        text:" Your code is searching for images on flickr."
        keywords
          p5.flickr.search
      concept
        text:" Your code is parsing or creating CSV files."
        keywords
          ~p5.csv.
          lambda2csv
      concept
        text:" Your code is semantically parsing HTML."
        keywords
          html2lambda
          p5.html.html2lambda
      concept
        text:" Your code is semantically creating HTML."
        keywords
          lambda2html
          p5.html.lambda2html
      concept
        text:" Your code seems to be directly manipulating images, make sure these images actually exists on disc."
        keywords
          ~p5.imaging.
      concept
        text:" Your code is creating HTTP requests, make sure you accommodate for networking errors by for instance wrapping your HTTP requests inside of a try catch block."
        keywords
          ~p5.http.
      concept
        text:" Your code is creating zip files."
        keywords
          zip
          p5.io.zip
      concept
        text:" Your code is parsing zip files."
        keywords
          unzip
          p5.io.unzip
      concept
        text:" Your code is semantically parsing Jason."
        keywords
          json2lambda
          p5.json.json2lambda
      concept
        text:" Your code is semantically creating JSON from a lambda object."
        keywords
          lambda2json
          p5.json.lambda2json
      concept
        text:" Your code is checking for emails using the pop 3 events in phosphorus five."
        keywords
          p5.pop3.get
      concept
        text:" Your code is sending emails."
        keywords
          p5.smtp.send
      concept
        text:" Your code is semantically parsing markdown, and transforming it into HTML."
        keywords
          markdown2html
          p5.markdown.markdown2html
      concept
        text:" Your code is using the MIME services of phosphorus five, to either create a MIME message, or to parse one."
        keywords
          ~p5.mime
      concept
        text:" Your code is creating PDF files from HTML."
        keywords
          html2pdf
          p5.pdf.html2pdf
      concept
        text:" Your code is using multiple threads, this is notoriously difficult to handle correctly, and I wish you good luck."
        keywords
          fork
          lock
          write-lock
          p5.threading.lock
          p5.threading.write-lock
          read-lock
          p5.threading.read-lock
          sleep
          p5.threading.sleep
          wait
          p5.threading.wait
      concept
        text:" Your code is creating XML snippets."
        keywords
          lambda2xml
          p5.xml.lambda2xml
      concept
        text:" Your code is semantically parsing XML snippets."
        keywords
          xml2lambda
          p5.xml.xml2lambda
      concept
        text:" Your code is dependent upon configuration settings from your app's configuration file, please make sure these configuration settings actually exists in your configuration file."
        keywords
          ~p5.config.
      concept
        text:" Your code is looping."
        keywords
          for-each
          while
      concept
        text:" Your code is branching."
        keywords
          if
          switch
      concept
        text:" Your code is modifying its execution tree."
        keywords
          set
          add
          insert-before
          insert-after
          apply
      concept
        text:" Your code is handling exceptions, good work. Creating defensive code is always smart."
        keywords
          throw
          try
          catch
          finally
      concept
        text:" Your code is doing mathematical operations."
        keywords
          \+
          \-
          \*
          \/
          \%
      concept
        text:" Your code is manipulating strings."
        keywords
          ends-with
          index-of
          join
          length
          match
          replace
          split
          starts-with
          to-lower
          to-upper
          trim

    /*
     * Looping through each [.concept], and checking if we can find it in our code.
     */
    for-each:x:/@.concepts/*
      _break:bool:false
      for-each:x:/@_dp/#/*/keywords/*?name
        if:x:/@p5.web.session.get/*/*/**/{0}
          :x:/@_dp?value

          /*
           * Concept found.
           */
          set:x:/@_break?value
            src:bool:true
          set:x:/@_extra?value
            src:{0}{1}
              :x:/@_extra?value
              :x:/..for-each/@_dp/#/*/text?value
          break
        if:x:/@_break?value
          set:x:/@_break?value
            src:bool:false
          break

    /*
     * Now we can return our concepts back to caller.
     */
    hypereval.text-feedback:These were the concepts I was able to find in your code.
    micro.speak:x:/@_extra?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing main root of code.
 */
analyse-idea:Discuss ideas
  action-name:Analyse ideas
  .is-report
  .evaluate

    /*
     * Extracting lambda from session, figuring out which different (known) ideas are
     * found in the code.
     */
    _extra
    p5.web.session.get:hypereval.analyze-lambda

    /*
     * List of (known) ideas.
     */
    .ideas
      idea
        text:" Your code is creating one or more Active Events, and should probably be made into a startup object, unless it already is, since otherwise you'll loose these events when your server is restarted."
        keywords
          create-event
          p5.events.create
      idea
        text:" Your code is creating widgets, and can probably successfully be turned into a page, unless you have already done this."
        keywords
          create-widget
          create-void-widget
          create-literal-widget
          create-container-widget
          p5.web.widgets.create
          p5.web.widgets.create-void
          p5.web.widgets.create-literal
          p5.web.widgets.create-container
          create-widgets
      idea
        text:" Your code is probably a web service or some sort, and should probably me made into a page, unless it already is."
        keywords
          p5.web.echo

    /*
     * Looping through each [.ideas], and checking if we can find it in our code.
     */
    for-each:x:/@.ideas/*
      _break:bool:false
      for-each:x:/@_dp/#/*/keywords/*?name
        if:x:/@p5.web.session.get/*/*/*/{0}
          :x:/@_dp?value

          /*
           * Concept found.
           */
          set:x:/@_break?value
            src:bool:true
          set:x:/@_extra?value
            src:{0}{1}
              :x:/@_extra?value
              :x:/..for-each/@_dp/#/*/text?value
          break
        if:x:/@_break?value
          set:x:/@_break?value
            src:bool:false
          break

    /*
     * Now we can return our concepts back to caller.
     */
    hypereval.text-feedback:These were the ideas I was able to find in your code.
    micro.speak:x:/@_extra?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing keywords in your code.
 */
quit:Thank you Elizabeth
  action-name:Quit
  .evaluate

    /*
     * Closing modal widget, clearing session, and ending dialogue, if widget exists.
     */
    if
      fetch:x:/0/0?value
        widget-exists:hypereval-analyze-modal
      delete-widget:hypereval-analyze-modal
    p5.web.session.set:hypereval.analyze-lambda
    micro.speak:No problem, live long and prosper
      voice:Karen
      pitch:1.1
      rate:0.85





/*
 * Creating a report.
 */
create-report:Can you create a report for me
  action-name:Create a report
  .evaluate

    /*
     * Acknowledging, and creating a report for the user.
     */
    micro.speak:Sure, I'll include it in your code.
      voice:Karen
      pitch:1.1
      rate:0.85

    /*
     * Creating our report.
     */
    p5.web.session.set:hypereval.report
    load-file:@HYPEREVAL/configuration/analyze-tree.hl
    for-each:x:/@load-file/*/*/*/.is-report/.

      /*
       * Modifying each [micro.speak] instance, to become our "append to report"
       * invocation instead.
       */
      set:x:/@_dp/#/**/micro.speak/*(!/)
      set:x:/@_dp/#/**/~micro.listen/*
      set:x:/@_dp/#/**/micro.speak?name
        src:hypereval._append-to-report

      /*
       * Evaluating [.evaluate], now modified.
       */
      eval:x:/@_dp/#/*/.evaluate

    /*
     * Adds our report comment at the top of the CodeMirror code.
     */
    hypereval._create-report

    /*
     * Closing modal widget, clearing session, and ending dialogue, if widget exists.
     */
    if
      fetch:x:/0/0?value
        widget-exists:hypereval-analyze-modal
      delete-widget:hypereval-analyze-modal
    p5.web.session.set:hypereval.analyze-lambda





/*
 * Easter eggs ...
 */
meaning-of-life:What is the meaning of life
  action-name:Meaning of life
  .invisible
  .evaluate

    /*
     * Funny, ha-ha ...!! :D
     */
    micro.speak:42
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

meaning-of-life-override:What's the meaning of life
  action-name:Meaning of life II
  .invisible
  .evaluate
    micro.speak:42, dude!
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

who-are-you:Who are you
  action-name:Who I am
  .invisible
  .evaluate

    /*
     * Identifying self.
     */
    micro.speak:My name is Elizabeth, and I am a senior system developer and a Homeopath
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

thank-you:Thank you
  action-name:Thank you
  .invisible
  .evaluate

    /*
     * Identifying self.
     */
    micro.speak:No problem
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

what-time-is-it:What time is it
  action-name:What time is it
  .invisible
  .evaluate

    /*
     * Easter egg'ish.
     */
    micro.speak:It is time to get rich
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

what-day-is-it:What day is it
  action-name:What day is it
  .invisible
  .evaluate

    /*
     * Speaking the day of the week.
     */
    p5.types.date.now
    p5.types.date.format:x:/-?value
      format:dddd
    micro.speak:It's a {0}
      :x:/@p5.types.date.format?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

how-do-you-feel:How do you feel
  action-name:How do you feel
  .invisible
  .evaluate

    /*
     * Easter egg'ish.
     */
    p5.types.date.now
    p5.types.date.format:x:/-?value
      format:dddd
    micro.speak:I feel very good, very fine, and very sexy!
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

fuck-you:F*** you
  action-name:Fuck you
  .invisible
  .evaluate

    /*
     * Easter egg'ish.
     */
    micro.speak:And fuck you too
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Closing modal widget, clearing session, and ending dialogue, if widget exists.
         */
        if
          fetch:x:/0/0?value
            widget-exists:hypereval-analyze-modal
          delete-widget:hypereval-analyze-modal
        p5.web.session.set:hypereval.analyze-lambda

        /*
         * Continuing our discussion.
         */
        p5.web.send-javascript:@"window.open('https://www.youtube.com/watch?v=fHC05_9b0gw', '_blank')"

you-re-an-asshole:You're an a******
  action-name:You're an asshole
  .invisible
  .evaluate

    /*
     * Easter egg'ish.
     */
    micro.speak:And fuck you too
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Closing modal widget, clearing session, and ending dialogue, if widget exists.
         */
        if
          fetch:x:/0/0?value
            widget-exists:hypereval-analyze-modal
          delete-widget:hypereval-analyze-modal
        p5.web.session.set:hypereval.analyze-lambda

        /*
         * Continuing our discussion.
         */
        p5.web.send-javascript:@"window.open('https://www.youtube.com/watch?v=UrgpZ0fUixs', '_blank')"

can-u-sing:Can you sing for me
  action-name:Can you sing for me
  .invisible
  .evaluate

    /*
     * Easter egg'ish.
     */
    micro.speak:do re mi fa so la ti to, tra-la-la
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

whats-your-favorite-song:What's your favourite song
  action-name:What's your favourite song
  .invisible
  .evaluate

    /*
     * Easter egg'ish.
     */
    micro.speak:I like all sorts of music, but here's one of my favourites.
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Closing modal widget, clearing session, and ending dialogue, if widget exists.
         */
        if
          fetch:x:/0/0?value
            widget-exists:hypereval-analyze-modal
          delete-widget:hypereval-analyze-modal
        p5.web.session.set:hypereval.analyze-lambda

        /*
         * Continuing our discussion.
         */
        p5.web.send-javascript:@"window.open('https://www.youtube.com/watch?v=KfvplbJnzao', '_blank')"

