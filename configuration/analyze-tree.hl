
/*
 * Contains our Hyperlambda analyze tree.
 */





/*
 * Analyzing keywords in your code.
 */
analyse-keywords:Discuss keywords
  action-name:Analyse keywords
  .evaluate

    /*
     * Extracting lambda from session, figuring out how many references to different
     * events the lambda contains, and giving user feedback about our findings.
     */
    _no:int:0
    .events
    p5.web.session.get:hypereval.analyze-lambda
    vocabulary
    set:x:/@vocabulary/*/=~.
    for-each:x:/@vocabulary/*?value

      /*
       * Checking if Active Event exists in lambda, and if so, bumping number of events.
       */
      if:x:/@p5.web.session.get/*/*/**/\{0}
        :x:/@_dp?value

        /*
         * Currently iterated Active Event was found in lambda from session.
         */
        set:x:/@_no?value
          +:x:/@_no?value
            _:1
        add:x:/@.events
          src:<strong>[{0}]</strong>
            :x:/@_dp?value

    /*
     * Providing visual feedback about which events we found.
     */
    join:x:/@.events/*?name
      sep:", "
    hypereval.text-feedback:We found the following keywords in your code {0}
      :x:/@join?value

    /*
     * Now we know how many events the code contains.
     */
    micro.speak:Your code contains {0} distinct keywords.
      :x:/@_no?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing events in your code.
 */
analyse-events:Discuss events
  action-name:Analyse keywords
  .evaluate

    /*
     * Extracting lambda from session, figuring out how many references to different
     * events the lambda contains, and giving user feedback about our findings.
     */
    _no:int:0
    .events
    p5.web.session.get:hypereval.analyze-lambda
    vocabulary
    for-each:x:/@vocabulary/*?value

      /*
       * Checking if Active Event exists in lambda, and if so, bumping number of events.
       */
      if:x:/@p5.web.session.get/*/*/**/\{0}
        :x:/@_dp?value

        /*
         * Currently iterated Active Event was found in lambda from session.
         */
        set:x:/@_no?value
          +:x:/@_no?value
            _:1
        add:x:/@.events
          src:<strong>[{0}]</strong>
            :x:/@_dp?value

    /*
     * Providing visual feedback about which events we found.
     */
    join:x:/@.events/*?name
      sep:", "
    hypereval.text-feedback:We found the following events in your code {0}
      :x:/@join?value

    /*
     * Now we know how many events the code contains.
     */
    micro.speak:Your code contains {0} Active Events.
      :x:/@_no?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing nodes in your code.
 */
analyse-nodes:Is it dry
  action-name:Analyse DRYness of code
  .evaluate

    /*
     * Extracting number of nodes, and number of unique nodes in lambda.
     */
    p5.web.session.get:hypereval.analyze-lambda

    /*
     * Providing visual feedback about results.
     */
    hypereval.text-feedback:There are {0} distinct nodes in your code.
      :x:/@p5.web.session.get/*/*/**/$?count

    /*
     * Figuring out ratio of unique nodes to nodes in total.
     */
    eval-x:x:/+|/+2
    _nodes:x:/@p5.web.session.get/*/*/**?count
    _unique-nodes:x:/@p5.web.session.get/*/*/**/$?count
    -:x:/@_nodes?value
      _:x:/@_unique-nodes?value
    -:x:/@_unique-nodes?value
      :x:/./-?value
    /:x:/@-?value.decimal
      _:x:/@_nodes?value.decimal
    *:x:/-?value
      _:int:100
    _extra
    if:x:/@*?value.int
      >:int:80
      set:x:/@_extra?value
        src:" Your code is very dry, and does not repeat itself much at all. This is very good."
    else-if:x:/@*?value.int
      >:int:50
      set:x:/@_extra?value
        src:" Your code is fairly dry, and does not repeat itself too much. This is OK."
    else-if:x:/@*?value.int
      >:int:20
      set:x:/@_extra?value
        src:" Your code is not dry, and does repeat itself too much. You should probably break it up more, by using loops, expressions, or by creating more supporting Active Events."

    /*
     * Now we know how many events the code contains.
     */
    micro.speak:You have {0} distinct nodes in your code, {1} nodes in total, and {2} percent of your nodes are unique. {3}
      :x:/@p5.web.session.get/*/*/**/$?count
      :x:/@p5.web.session.get/*/*/**?count
      :x:/@*?value.int
      :x:/@_extra?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing comments in your code.
 */
analyse-commenting:Comments
  action-name:Analyse comments
  .evaluate

    /*
     * Extracting code and lambda from session, figuring out the ratio of comments
     * to nodes, and providing feedback to user.
     */
    p5.web.session.get:hypereval.analyze-lambda
    lambda2hyper:x:/@p5.web.session.get/*/*/*
    p5.web.session.get:hypereval.analyze-code

    /*
     * Figuring out ratio of comments to code.
     */
    length:x:/@lambda2hyper?value
    length:x:/@p5.web.session.get/*?value
    /:x:/../*/length/[0,1]?value.decimal
      _:x:/../*/length/[1,2]?value.decimal
    *:x:/-?value
      _:decimal:100
    -:int:100
      _:x:/@*?value.int
    _extra
    if:x:/@-?value.int
      >:int:80
      set:x:/@_extra?value
        src:" You have commented your code very well."
    else-if:x:/@-?value.int
      >:int:50
      set:x:/@_extra?value
        src:" You have commented your code fairly well."
    else-if:x:/@-?value.int
      >:int:20
      set:x:/@_extra?value
        src:" You should comment your code more."
    else
      set:x:/@_extra?value
        src:" This is not good, not good at all."


    /*
     * Providing visual feedback about which events we found.
     */
    hypereval.text-feedback:{0} percent is comments.
      :x:/@-?value.int

    /*
     * Now we know how many events the code contains.
     */
    micro.speak:{0} percent of your code is comments. {1}
      :x:/@-?value.int
      :x:/@_extra?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing concepts in your code.
 */
analyse-concepts:Discuss concepts
  action-name:Analyse concepts
  .evaluate

    /*
     * Extracting lambda from session, figuring out which different (known) concepts are
     * found in the code.
     */
    _extra
    p5.web.session.get:hypereval.analyze-lambda

    /*
     * List of (known) concepts.
     */
    .concepts
      concept
        text:" Your code is accessing your my sequel database."
        keywords
          ~p5.mysql.
      concept
        text:" Your code is creating widgets."
        keywords
          create-widget
          create-void-widget
          create-literal-widget
          create-container-widget
          p5.web.widgets.create
          p5.web.widgets.create-void
          p5.web.widgets.create-literal
          p5.web.widgets.create-container
          create-widgets
      concept
        text:" Your code is accessing your session collection."
        keywords
          ~p5.web.session
      concept
        text:" Your code is accessing your view state collection."
        keywords
          ~p5.web.viewstate
      concept
        text:" Your code is accessing your cookie collection, which means it seems like it is dependent upon state, across multiple http requests."
        keywords
          ~p5.web.cookie
      concept
        text:" Your code is accessing your http post parameter collection."
        keywords
          ~p5.web.post.
      concept
        text:" Your code is accessing the raw http request, which implies it can possibly be invoked by clients that are not browsers."
        keywords
          p5.web.request.get-method
          p5.web.request.get-body
          p5.web.request.parse-mime
          p5.web.request.save-body
          ~p5.web.header
      concept
        text:" Your code seems to be echoing its own response back to the client, which implies it's some sort of web service, and can be invoked by clients that are not browsers."
        keywords
          p5.web.echo
      concept
        text:" Your code seems to be echoing a file back to the client. Be on the lookout here for not accidentally echoing files back to the client, which allows a malicious adversary to gain access to information he shouldn't gain access to."
        keywords
          p5.web.echo-file
      concept
        text:" Your code seems to be using inline CSS styles. You might want to consider using CSS classes instead, which is arguably better, since it separates representation from content."
        keywords
          create-widget/**/style
          create-void-widget/**/style
          create-literal-widget/**/style
          create-container-widget/**/style
          p5.web.widgets.create/**/style
          p5.web.widgets.create-void/**/style
          p5.web.widgets.create-literal/**/style
          p5.web.widgets.create-container/**/style
          create-widgets/**/style
      concept
        text:" Your code seems to be handling Ajax events."
        keywords
          create-widget/**/~on
          create-void-widget/**/~on
          create-literal-widget/**/~on
          create-container-widget/**/~on
          p5.web.widgets.create/**/~on
          p5.web.widgets.create-void/**/~on
          p5.web.widgets.create-literal/**/~on
          p5.web.widgets.create-container/**/~on
          create-widgets/**/style
      concept
        text:" Your code seems to be creating events, which modifies the state of your server. Be on alert here, such that you make sure your events have a unique name space, such that they don't clash with other pre existing events."
        keywords
          create-event
          p5.events.create
      concept
        text:" Your code seems to be deleting events, which modifies the state of your server. Be on alert here, such that you don't accidentally delete events you are dependent upon here."
        keywords
          delete-event
          p5.events.delete
      concept
        text:" Your code is accessing your phosphorus five memory based database."
        keywords
          select-data
          insert-data
          append-data
          delete-data
          p5.data.select
          p5.data.insert
          p5.data.append
          p5.data.delete
      concept
        text:" Your code is dependent upon Micro."
        keywords
          ~micro.
      concept
        text:" Your code is evaluating code, which implies you should be carefully looking for unintentional side-effects, such as evaluating malicious code by accident."
        keywords
          eval
          eval-mutable
      concept
        text:" Your code is evaluating code, but since it does this using a whitelist, this is probably OK."
        keywords
          eval-whitelist

    /*
     * Looping through each [.concept], and checking if we can find it in our code.
     */
    for-each:x:/@.concepts/*
      _break:bool:false
      for-each:x:/@_dp/#/*/keywords/*?name
        if:x:/@p5.web.session.get/*/*/**/{0}
          :x:/@_dp?value

          /*
           * Concept found.
           */
          set:x:/@_break?value
            src:bool:true
          set:x:/@_extra?value
            src:{0}{1}
              :x:/@_extra?value
              :x:/..for-each/@_dp/#/*/text?value
          break
        if:x:/@_break?value
          set:x:/@_break?value
            src:bool:false
          break

    /*
     * Now we can return our concepts back to caller.
     */
    hypereval.text-feedback:These were the concepts I was able to find in your code.
    micro.speak:x:/@_extra?value
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing





/*
 * Analyzing keywords in your code.
 */
quit:Thank you Elizabeth
  action-name:Quit
  .evaluate

    /*
     * Closing modal widget, clearing session, and ending dialogue, if widget exists.
     */
    if
      fetch:x:/0/0?value
        widget-exists:hypereval-analyze-modal
      delete-widget:hypereval-analyze-modal
    p5.web.session.set:hypereval.analyze-lambda
    micro.listen.quit
    micro.speak:No problem, I am happy to be of assistance.
      voice:Karen
      pitch:1.1
      rate:0.85





/*
 * Easter eggs ...
 */
meaning-of-life:What is the meaning of life
  action-name:Meaning of life
  .invisible
  .evaluate

    /*
     * Funny, ha-ha ...!! :D
     */
    micro.speak:42
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing

who-are-you:Who are you
  action-name:Who I am
  .invisible
  .evaluate

    /*
     * Identifying self.
     */
    micro.speak:My name is Elizabeth, and I am a senior system developer and a Homeopath
      voice:Karen
      pitch:1.1
      rate:0.85
      onfinish

        /*
         * Continuing our discussion.
         */
        hypereval._continue-analyzing
