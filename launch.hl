
/*
 * File responsible for starting "Hypereval".
 */





/*
 * Checking if this is a request for a specific page.
 */
split:x:/@split/0?name
  =:/
if:x:/@split/*?count
  >:int:1
  p5.mysql.connect:[hypereval]
    p5.mysql.select:@"select content from snippets where name = @name and type = 'page'"
      @name:x:/@split/1?name

    /*
     * Verifying page actually exists.
     */
    if:x:/@p5.mysql.select/*
      not

      /*
       * No such page.
       */
      p5.web.response.set-status-code:404
      p5.web.echo:This is where URLs goes to die ...
      return

    /*
     * Transforming Hyperlambda into lambda and evaluating it, before we return early,
     * to avoid evaluation of the rest of the file.
     */
    hyper2lambda:x:/@p5.mysql.select/*/*/content?value
    eval:x:/@hyper2lambda
    return

else

  /*
   * This is not a request for a specific page, hence we load up main wireframe for
   * Hypereval.
   */
  add:x:/./*/micro.evaluate.file
    src:x:/../*(/chrome|/url)
  micro.evaluate.file:@HYPEREVAL/helpers/launch-hypereval.hl
