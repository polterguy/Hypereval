
/*
 * File responsible for starting "Hypereval".
 *
 * This file is evaluated when the user clicks the "desktop icon" for the "Hypereval" app.
 */





/*
 * Including Micro and its "serious" skin.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@MICRO/media/fonts.css





/*
 * Default values, if no settings have been applied.
 */
.defaults
  font-size:9.25





/*
 * Selecting settings, if any.
 */
select-data:x:/*/*/hypereval.settings





/*
 * Verifying user is logged in as root, and if not, 
 * forcing the user to login before we proceed.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root

  /*
   * User is not logged in as root.
   */
  p5.core.login
    message:You'll need to login with a root account to access this module

  /*
   * Returning early to abort evaluating the rest of our file.
   */
  return





/*
 * Checking if this is an upload request.
 */
split:x:/../*/url?value
  =:/
if:x:/@split/0/-?name
  =:upload

  /*
   * Parsing MIME message, and reading file, and stuffing content into session.
   */
  p5.web.request.parse-mime
    attachment-folder:~/temp/
  load-file:{0}{1}{2}
    :x:/@p5.web.request.parse-mime/**/filename/[0,1]/*/folder?value
    :x:/@p5.web.request.parse-mime/**/filename/[0,1]/*/prefix?value
    :x:/@p5.web.request.parse-mime/**/filename/[0,1]?value
    convert:false
  p5.web.session.set:hypereval.uploaded-file
    src:x:/@load-file/*?value
  split:x:/@p5.web.request.parse-mime/**/filename/[0,1]?value
    =:.
  p5.web.session.set:hypereval.snippet-name
    src:x:/@split/0?name
  p5.web.session.set:hypereval.code
    src:x:/@load-file/*?value

  /*
   * Returning early to avoid evaluation of the rest of our file.
   */
  p5.web.echo:SUCCESS
  return





/*
 * Creating main content container.
 */
create-widget:hypereval-main-container
  class:container
  widgets
    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:right
              widgets
                div
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    button
                      innerValue:@"<span class=""icon-cog""></span>"
                      title:Settings ...
                      onclick

                        /*
                         * Showing a modal window, allowing user to modify his settings.
                         */
                        create-widgets
                          micro.widgets.modal:hypereval-settings-modal
                            widgets
                              h3
                                innerValue:Settings
                              micro.widgets.wizard-form:hypereval-settings-data
                                text
                                  info:Font size
                                  .data-field:font-size
                                  type:number
                                  min:8
                                  max:20
                                  step:.25
                                  onkeydown:@"if (event.keyCode == 13) {p5.$('hypereval-settings-ok-button').raise('onclick');return false;}"
                                  oninit

                                    /*
                                     * Making sure we set value to whatever it already is, if anything.
                                     */
                                    .defaults
                                      font-size:9.25
                                    select-data:x:/*/*/hypereval.settings
                                    set-widget-property:x:/../*/_event?value
                                      value:x:(/@select-data/*/*/font-size|/@.defaults/*/font-size)/$?value

                                    /*
                                     * Setting initial focus to font-size widget.
                                     */
                                    micro.page.set-focus:x:/../*/_event?value

                                div
                                  class:right
                                  widgets
                                    button:hypereval-settings-ok-button
                                      style:"margin-bottom:0;"
                                      innerValue:OK
                                      onclick

                                        /*
                                         * Serializing form, saving settings to p5.data database, 
                                         * and applying new settings.
                                         */
                                        micro.widgets.wizard-form.value:hypereval-settings-data
                                        delete-data:x:/*/*/hypereval.settings
                                        add:x:/../*/insert-data/*
                                          src:x:/@micro.widgets.wizard-form.value/*
                                        insert-data
                                          hypereval.settings
                                        set-widget-property:hypereval-codemirror-wrapper
                                          style:"font-size:{0}pt;"
                                            :x:/@micro.widgets.wizard-form.value/*/font-size?value

                                        /*
                                         * Refreshing page, since CodeMirror doesn't handle it very well
                                         * when font is changed dynamically.
                                         */
                                        p5.web.reload-location

                    button
                      innerValue:@"<span class=""icon-home""></span>"
                      title:Close Hypereval
                      onclick

                        /*
                         * Redirecting user to server's root URL.
                         */
                        p5.web.get-root-location
                        p5.web.set-location:x:/-?value

    div
      class:row
      widgets
        div:hypereval-codemirror-wrapper
          class:col-100
          style:"font-size:{0}pt;"
            :x:(/@select-data/*/*/font-size|/../*/.defaults/*/font-size)/$?value
          widgets
            hypereval.widgets.eval
