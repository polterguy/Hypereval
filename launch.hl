
/*
 * File responsible for starting "Hypereval".
 */





/*
 * Signal node, to separate arguments from the rest of lambda.
 */
.signal





/*
 * Checking if this is a request for a specific "page" snippet.
 */
split:x:/../*/url?value
  =:?
split:x:/@split/0?name
  =:/
if:x:/@split/*?count
  >:int:1

  /*
   * This is a request for a "page snippet".
   *
   * Evaluating snippet, making sure we wrap our evaluation in a try/catch block,
   * such that we can return intelligent error feedback to client if snippet doesn't exist.
   */
  try

    /*
     * Invoking event responsible for evaluating the requests page snippet.
     */
    hypereval.snippets.evaluate:x:/@split/1?name
      type:page

  catch

    /*
     * No such snippet found.
     */
    p5.web.response.set-status-code:404
    p5.web.response.set-status-description:404 - No such snippet found
    p5.web.echo:No such snippet found

else

  /*
   * This is not a request for a specific page, hence we load up main wireframe for
   * Hypereval.
   */
  add:x:/./*/micro.evaluate.file
    src:x:/@.signal/--/<-
  micro.evaluate.file:@HYPEREVAL/helpers/launch-hypereval.hl
