/*
 * Starts main "Hypereval" app.
 */





/*
 * Checking if Hypereval is already running, and if so, throwing an exception.
 */
if
  hypereval.widgets.eval.is-running
  throw:Hypereval is already running





/*
 * Including Micro, selected skin, and font CSS file.
 */
micro.css.include





/*
 * Setting title of page.
 */
p5.web.page.set-title:Hypereval





/*
 * Creating main wireframe for module.
 */
create-widget
  class:container
  widgets
    div
      class:row
      widgets

        /*
         * Wrapper widget for both H1 (header) and toolbar widget.
         */
        div
          class:col-100
          widgets
            h1
              innerValue:Hypereval <small>{Hyperlambda snippets evaluator}</small>
              style:"float:left;"
              events

                /*
                 * Sets the header text of module.
                 */
                hypereval.set-header

                  /*
                   * Checking if caller provided an argument, or if he simply wants
                   * to "delete" existing header value.
                   */
                  if:x:/../*/_arg?value
                    and:x:/../*/_arg?value
                      !=:

                    /*
                     * Setting h1 element's value to whatever was provided as [_arg].
                     */
                    set-widget-property:x:/../*/_event?value
                      innerValue:Hypereval <small>{{{0}}}</small>
                        :x:/../*/_arg?value

                  else

                    /*
                     * No [_arg] given, setting H1 element's value to "default value".
                     */
                    set-widget-property:x:/../*/_event?value
                      innerValue:Hypereval <small>{Hyperlambda snippets evaluator}</small>


            /*
             * Toolbar wrapper widget.
             */
            micro.widgets.folder-plugin
              class:strip right toolbar
              folder:@HYPEREVAL/helpers/top-toolbar-buttons/

    div
      class:row
      widgets
        div
          class:col-100
          widgets

            /*
             * Our actual CodeMirror wrapper widget.
             */
            hypereval.widgets.eval
