
/*
 * Save snippet button.
 */
button:hypereval-save-snippet
  innerValue:@"<span class=""icon-floppy-disk""></span>"
  title:Saves snippet - Alt+S
  onclick

    /*
     * Checking if we should provide a default alternative value for "type" of snippet
     * radio buttons.
     */
    p5.web.session.get:hypereval.snippet-name
    if:x:/@p5.web.session.get/*?value
      p5.mysql.connect:[hypereval]
        p5.mysql.scalar:@"select type from snippets where name = @name"
          @name:x:/@p5.web.session.get/*?value
        if:x:/@p5.mysql.scalar?value
          add:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/={0}
            :x:/@p5.mysql.scalar?value
            src:checked

    /*
     * Making sure we check of "Snippet" radio button, if no other radio buttons have been checked
     * by above logic.
     */
    if:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/checked
      not
      add:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/=snippet
        src:checked

    /*
     * Saving Hyperlambda to database, by allowing user to associate a name with snippet,
     * through a modal window, for then to save the code with the specified name.
     */
    create-widgets
      micro.widgets.modal:hypereval-save-modal
        widgets
          h3
            innerValue:Supply a name please
          p
            innerValue:And choose if you want to save your snippet as a page or a startup snippet. Notice, if you supply a name of a snippet that already exists, the old snippet will be overwritten.
          micro.widgets.wizard-form:hypereval-save-form
            text
              info:Name
              .data-field:name
              onkeydown:@"if (event.keyCode == 13) {p5.$('hypereval-save-modal-button').raise('onclick');return false;}"
              oninit

                /*
                 * Setting initial focus to "Name" textbox.
                 */
                micro.page.set-focus:x:/../*/_event?value

                /*
                 * Checking if we have a snippet name in session, and
                 * if so, we make sure we default our name to that value.
                 */
                p5.web.session.get:hypereval.snippet-name
                if:x:/-/*?value
                  set-widget-property:x:/../*/_event?value
                    value:x:/@p5.web.session.get/*?value

            radio-group
              .data-field:type
              options
                Snippet:snippet
                Page:page
                Startup:startup

            div
              class:right
              style:"margin-top:-1rem;"
              widgets
                div
                  class:strip
                  widgets
                    button:hypereval-save-modal-button
                      innerValue:Save
                      title:Saves your snippet
                      onclick

                        /*
                         * Verfying there actually is any code.
                         */
                        get-widget-property:hypereval-input
                          innerValue
                        if
                          trim:x:/@get-widget-property/*/*?value
                          =:

                          /*
                           * No code.
                           */
                          micro.windows.info:There is no code to save
                            class:micro-windows-info warning
                          return

                        /*
                         * Serializing form, opening database connection,
                         * deleting snippets with similar name, and saving snippet
                         * into database.
                         */
                        micro.form.serialize:hypereval-save-form

                        /*
                         * Sanity checking name of snippet.
                         */
                        match:x:/@micro.form.serialize/*/name?value
                          src:regex:/^[-a-z0-9_ \.]{2,}$/i
                        if:x:/@match/*?name
                          not
                          micro.windows.info:Use at least 2 characters, only spaces, a-z, 0-9 and '-' in your name
                            class:micro-windows-info warning
                          return

                        /*
                         * Inserting snippet into database.
                         */
                        p5.mysql.connect:[hypereval]
                          p5.mysql.delete:@"delete from snippets where name = @name"
                            @name:x:/@micro.form.serialize/*/name?value
                          p5.mysql.insert:@"insert into snippets (name, content, type) values (@name, @content, @type)"
                            @name:x:/@micro.form.serialize/*/name?value
                            @content:x:/@get-widget-property/*/*?value
                            @type:x:/@micro.form.serialize/*/type?value

                        /*
                         * Closing modal window, and giving user some feedback.
                         */
                        delete-widget:hypereval-save-modal
                        micro.windows.info:Snippet was successfully saved
                          class:micro-windows-info success

                        /*
                         * Making sure we store name used into session, in case
                         * user wants to save the same snippet again.
                         */
                        p5.web.session.set:hypereval.snippet-name
                          src:x:/@micro.form.serialize/*/name?value

                        /*
                         * Storing code in session, to make it more easily available.
                         */
                        p5.web.session.set:hypereval.code
                          src:x:/../*/get-widget-property/*/*?value

                        /*
                         * Signaling that snippet was changed.
                         */
                        hypereval.snippet.changed

                        /*
                         * Making sure we disable delete snippet button, 
                         * since there are no active snippets now.
                         */
                        delete-widget-property:hypereval-delete-snippet
                          disabled

                        /*
                         * Invoking event responsible for updating our header.
                         */
                        hypereval.set-header:x:/@micro.form.serialize/*/name?value

                    button
                      innerValue:Cancel
                      title:Closes your save window without saving
                      onclick

                        /*
                         * Simply closing modal window.
                         */
                        delete-widget:hypereval-save-modal
