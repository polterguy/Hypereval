
/*
 * Deletes currently loaded snippet, asking user to confirm deletion first.
 *
 * First verifying that there actually is a snippet currently loaded.
 */
p5.web.session.get:hypereval.snippet-name
if:x:/@p5.web.session.get/*?value
  not
  micro.windows.info:There is no snippet currently loaded
    class:micro-windows-info warning
  return





/*
 * Showing a modal widget, asking user to confirm deletion.
 */
create-widgets
  micro.widgets.modal:hypereval-confirm-snippet-deletion
    widgets
      h3
        innerValue:Confirm deletion
      p
        innerValue:Are you sure you want to delete this snippet? This action is permanent.
      div
        class:right
        widgets
          div
            class:strip
            widgets
              button:hypereval-confirm-deletion-btn
                innerValue:Yes
                oninit

                  /*
                   * Setting initial focus to "Yes" button.
                   */
                  micro.page.set-focus:x:/../*/_event?value

                onclick

                  /*
                   * Figuring out which snippet user wants to delete, if any.
                   */
                  p5.web.session.get:hypereval.snippet-name
                  if:x:/@p5.web.session.get/*?value
                    not
                    or:x:/@p5.web.session.get/*?value
                      =:

                    /*
                     * Snippet is probably not saved yet.
                     */
                    return

                  /*
                   * Opening database, and deleting snippet from snippet collection.
                   */
                  p5.mysql.connect:[hypereval]
                    p5.mysql.delete:@"delete from snippets where name = @name"
                      @name:x:/@p5.web.session.get/*?value

                  /*
                   * Removing code from editor.
                   */
                  hypereval.widgets.eval.set-code

                  /*
                   * Cleaning out session variables.
                   */
                  p5.web.session.set:hypereval.snippet-name

                  /*
                   * Making sure we disable delete snippet button, 
                   * since there are no active snippets now.
                   */
                  set-widget-property:hypereval-delete-snippet
                    disabled

                  /*
                   * Deleting modal window.
                   */
                  delete-widget:hypereval-confirm-snippet-deletion

                  /*
                   * Disabling preview pagebutton.
                   */
                  set-widget-property:hypereval-preview-snippet
                    disabled

                  /*
                   * Setting header with empty value, changes it to its default value.
                   */
                  hypereval.set-header

              button
                innerValue:No
                onclick

                  /*
                   * Simply deleting modal window.
                   */
                  delete-widget:hypereval-confirm-snippet-deletion
