
/*
 * Creates evaluate snippet Active Event.
 *
 * This event will allow you to evaluate a snippet by name.
 * Pass in the name of the snippet you want to evaluate as [_arg].
 * Optionally pass in [type] being for instance "page" or "startup".
 *
 * Notice, this event allows for creating extension widgets that returns
 * widgets to caller, allowing for the evaluation of a snippet, to create
 * an extension widget.
 */
create-event:hypereval.snippets.evaluate

  /*
   * Separating arguments from the rest of the lambda.
   */
  .signal

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string
  micro.lambda.contract.optional:x:/..
    type:string

  /*
   * Retrieving snippet.
   */
  add:x:/+
    src:x:/@.signal/--/<-(/type|/_arg)
  hypereval.snippets.load

  /*
   * Converting snippet to Hyperlambda, passing in all arguments (except _arg and type),
   * and evaluating snippet.
   */
  hyper2lambda:x:/@hypereval.snippets.load?value
  add:x:/+
    src:x:/@.signal/--(!/_arg!/type)
  eval:x:/@hyper2lambda

  /*
   * Returning results back to caller.
   */
  set:x:/./*/return?value
    src:x:/@eval?value
  add:x:/./*/return
    src:x:/@eval/*
  return
