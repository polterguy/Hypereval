
/*
 * Creates a new snippet.
 *
 * It expects the snippet's name to be given as [_arg], and its Hyperlambda to
 * be found in [content]. In addition, you can optionally pass in [type] being
 * the type of snippet, which can be e.g. "page", "startup" or "snippet".
 * The default [type] is "snippet".
 *
 * Notice, you can (optionally) avoid giving [content] a value, and instead pass
 * in lambda as children of [content], which then will be converted into Hyperlambda,
 * and treated as content of your snippet.
 */
create-event:hypereval.snippets.save

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string
    content
  micro.lambda.contract.optional:x:/..
    type:string

  /*
   * Defaults.
   */
  .defaults
    type:snippet

  /*
   * Sanity checking name of snippet.
   */
  match:x:/../*/_arg?value
    src:regex:/^[-a-z0-9_ \.]{2,}$/i
  if:x:/@match/*?name
    not
    throw:Illegal snippet name. Use at least 2 characters, only spaces, a-z, A-Z, 0-9, '.', '_', ' ' and '-' in your snippet's name

  /*
   * Sanity checking [content].
   */
  if:x:/../*/content?value
    =:
    throw:No [content] value provided while trying to save snippet '{0}'
      :x:/../*/_arg?value

  /*
   * Checking if Hyperlambda was provided as value of [content].
   */
  if:x:/../*/content?value

    /*
     * Making sure snippet is a valid piece of Hyperlambda.
     *
     * This event invocation will throw and exception, if snippet is not valid Hyperlambda.
     */
    hyper2lambda:x:/../*/content?value

  else

    /*
     * Assuming [content] is a lambda object.
     */
    lambda2hyper:x:/../*/content/*
    set:x:/../*/content?value
      src:x:/@lambda2hyper?value

  /*
   * Opening up database connection, and saving our snippet to the database.
   */
  p5.mysql.connect:[hypereval]

    /*
     * Inserting new snippet.
     */
    p5.mysql.insert:@"insert into snippets (name, content, type) values (@name, @content, @type) on duplicate key update content = @content, type = @type"
      @name:x:/../*/_arg?value
      @content:x:/../*/content?value
      @type:x:(/../*/type|/@.defaults/*/type)/$?value
