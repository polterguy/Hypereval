
/*
 * Retrieves the Hyperlambda for the snippet(s) with the specified [_arg] name(s).
 *
 * This event will return the Hyperlambda of all snippets matching the specified [_arg].
 * Optionally pass in [type] of snippet being e.g. "page" or "startup".
 *
 * The snippet(s) Hyperlambda will be found in a [content] return node.
 * One [content] for each snippet requested will be returned to caller.
 */
create-event:hypereval.snippets.load

  /*
   * Separating arguments from the rest of the lambda.
   */
  .signal

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string
  micro.lambda.contract.optional:x:/..
    type:string

  /*
   * Checking if caller supplied an (optional) [type] declaration.
   */
  _type-where
  if:x:/@.signal/--/type

    /*
     * Caller supplied an optional [type] declaration.
     *
     * Making sure we further parametrize SQL with requested [type].
     */
    set:x:/@_type-where?value
      src:"and type = @type"
    add:x:/../*/p5.mysql.connect/*/for-each/*/p5.mysql.scalar
      src
        @type:x:/@.signal/--/type?value

  /*
   * Opening up database connection, selecting snippet, and returning its content
   * as [content] to caller.
   */
  p5.mysql.connect:[hypereval]

    /*
     * Looping through each [_arg] specified by caller, and concatenating into
     * [_result].
     */
    for-each:x:/../*/_arg?value

      /*
       * Selecting currently iterated snippet.
       */
      p5.mysql.scalar:@"select content from snippets where name = @name {0}"
        :x:/@_type-where?value
        @name:x:/@_dp?value

      /*
       * Making sure that snippet actually exists.
       */
      if:x:/@p5.mysql.scalar?value
        not
        throw:The snippet '{0}' doesn't exist
          :x:/@_dp?value

      /*
       * Making sure we return currently iterated snippet's Hyperlambda
       * as [content] back to caller.
       */
      eval-x:x:/+/*/*
      add:x:/../*/return
        src
          content:x:/@p5.mysql.scalar?value

  /*
   * Returning snippet to caller.
   */
  return
