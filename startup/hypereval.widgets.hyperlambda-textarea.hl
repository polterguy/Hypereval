
/*
 * Responsible for creating the CodeMirror Hyperlambda textarea widget.
 *
 * All arguments passed in will be added to main root widget, except
 * [auto-focus], [height] and [value], which will be used to determine editor's 
 * initial value, height, and whether or not it should automatically get focus or 
 * not.
 *
 * To set its initial value, pass in [value] as its initial code value.
 */
create-event:hypereval.widgets.hyperlambda-textarea

  /*
   * Used to separate arguments from the rest of our lambda.
   */
  .signal

  /*
   * Sanity check.
   */
  micro.lambda.contract.optional:x:/..
    auto-focus:bool
    value:string
    height:string

  /*
   * Figuring out font size to use for widget, and other settings.
   */
  .defaults
    font-size:9.25
    theme:hyperlambda
    line-numbers:bool:true
    auto-focus:bool:false
    height:300px
  select-data:x:/*/*/hypereval.settings

  /*
   * Decorating widget with arguments.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--(!/_arg!/value)
  add:x:/../*/return/*/container/*/widgets/*/literal
    src:x:/@.signal/--/value

  /*
   * Returning widget to caller.
   */
  eval-x:x:/+/*/*/style|/+/**(/.theme|/.line-numbers|/.auto-focus|/.height)
  return
    container
      style:"font-size:{0}pt;"
        :x:(/@select-data/*/*/font-size|/../*/.defaults/*/font-size)/$?value
      widgets
        literal
          element:textarea
          oninit

            /*
             * Forward evaluated above.
             */
            .height:x:(/@.signal/--/height|/@.defaults/*/height)/$?value

            /*
             * Including CodeMirror JavaScript.
             */
            p5.web.include-javascript-file
              @HYPEREVAL/media/codemirror-all.min.js
              @HYPEREVAL/media/codemirror/mode/hyperlambda/hyperlambda.min.js

            /*
             * Making sure necessary Stylesheet files are included
             */
            .theme:x:(/@select-data/*/*/theme|/../*/.defaults/*/theme)/$?value
            p5.web.include-css-file:@HYPEREVAL/media/codemirror-all.min.css
            p5.web.include-css-file:@HYPEREVAL/media/codemirror/theme/{0}.css
              :x:/@.theme?value

            /*
             * Including AutoCompletion JSON.
             */
            hypereval._internals.include-auto-complete

            /*
             * Transforming input textarea to CodeMirror instance.
             */
            .line-numbers:x:(/@select-data/*/*/line-numbers|/../*/.defaults/*/line-numbers)/$?value
            set:x:/-?value
              to-lower:x:/./-?value.string
            .auto-focus:x:(/../*/auto-focus|/../*/.defaults/*/auto-focus)/$?value
            set:x:/-?value
              to-lower:x:/./-?value.string
            p5.io.unroll-path:@HYPEREVAL/
            p5.web.send-javascript:@"p5['{0}'] = CodeMirror.fromTextArea(p5.$('{0}').el, {{
  mode:'hyperlambda',
  theme:'{2}',
  lineNumbers:{3},
  styleActiveLine:true,
  path:'{1}/media/codemirror/',
  autofocus:{4},
  tabSize:2,
  indentAuto:true,
  extraKeys: {{
    'Ctrl-Space':'autocomplete',
    'Shift-Tab':'indentLess',
    'Tab':'indentMore'
  }}
}});
p5['{0}'].on('change',function (cMirror) {{
  p5.$('{0}').el.value = cMirror.getValue();
}});
p5['{0}'].setSize('100%', '{5}');"
              :x:/../*/_event?value
              :x:/@p5.io.unroll-path?value
              :x:/@.theme?value
              :x:/@.line-numbers?value
              :x:/@.auto-focus?value
              :x:/@.height?value
