
/*
 * Responsible for creating the CodeMirror Hyperlambda evaluator widget.
 */
create-event:hypereval.widgets.eval

  /*
   * Used to separate arguments from the rest of our lambda.
   */
  .signal

  /*
   * Decorating widget with arguments.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--(!/_arg)

  /*
   * Returning widget to caller.
   */
  return
    container
      style:"margin-bottom:20px;"
      events

        /*
         * Helper method to determine if Hypereval is already loaded on page.
         */
        hypereval.widgets.eval.exists
          return:bool:true

        /*
         * Helper method to programmatically load the named snippet.
         *
         * Expects [_arg] to have snippet's name.
         */
        hypereval.widgets.eval.load

          /*
           * Using API method to make it simple.
           */
          hypereval.snippets.get:x:/../*/_arg?value

          /*
           * Modifying textarea's and CodeMirror's values.
           */
          set-widget-property:hypereval-input
            innerValue:x:/@hypereval.snippets.get?value
          p5.web.send-javascript:@"p5.hypereval_input.getDoc().setValue(`{0}`);"
            :x:/@hypereval.snippets.get?value

          /*
           * Storing into session the snippet's name, and its code.
           */
          p5.web.session.set:hypereval.snippet-name
            src:x:/../*/_arg?value
          p5.web.session.set:hypereval.code
            src:x:/@hypereval.snippets.get?value

      oninit

        /*
         * Making sure our page becomes a "dropzone" to allow user to upload 
         * a Hyperlambda file (which will populate the CodeMirror module with its code).
         */
        p5.io.unroll-path:@HYPEREVAL/
        split:x:/@p5.io.unroll-path?value
          =:/
        eval-x:x:/+/*/url
        micro.page.create-dropzone
          filter:hl|zip
          multiple:false
          url:{0}/upload
            :x:/@split/0/-?name
          .onfinish:@"function(){p5.$('hypereval-evaluate-button').raise('.onupload',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_input.getDoc().setValue(ret.code);}}});}"

        /*
         * Including CodeMirror JavaScript.
         */
        p5.web.include-javascript-file
          @HYPEREVAL/media/codemirror/lib/codemirror.js
          @HYPEREVAL/media/codemirror/addon/selection/active-line.js
          @HYPEREVAL/media/codemirror/addon/display/fullscreen.js
          @HYPEREVAL/media/codemirror/addon/hint/show-hint.js
          @HYPEREVAL/media/codemirror/mode/hyperlambda/hyperlambda.js

        /*
         * Making sure necessary Stylesheet files are included
         */
        p5.web.include-css-file
          @HYPEREVAL/media/codemirror/lib/codemirror.css
          @HYPEREVAL/media/codemirror/theme/hyperlambda.css
          @HYPEREVAL/media/codemirror/addon/hint/show-hint.css
          @HYPEREVAL/media/codemirror/addon/display/fullscreen.css

        /*
         * Including Active Events JSON on page.
         */
        vocabulary
        join:x:/@vocabulary/*?value
          sep:,
          wrap:'
        p5.web.include-javascript:@"CodeMirror._hyperlispKeywords = [{0}];"
          :x:/@join?value

        /*
         * Transforming input textarea to CodeMirror instance.
         */
        p5.io.unroll-path:@HYPEREVAL/
        p5.web.send-javascript:@"p5.hypereval_input = CodeMirror.fromTextArea(p5.$('hypereval-input').el, {{
  mode:'hyperlambda',
  theme:'hyperlambda',
  lineNumbers:true,
  styleActiveLine:true,
  path:'{0}/media/codemirror/',
  autofocus:true,
  tabSize:2,
  indentAuto:true,
  extraKeys: {{
    'Ctrl-Space':'autocomplete',
    'Shift-Tab':'indentLess',
    'Tab':'indentMore',
    'Alt-F':function(cm) {{cm.setOption('fullScreen', !cm.getOption('fullScreen'));}},
    'Alt-E':function(cm) {{
      p5.$('hypereval-evaluate-button').raise('.onclick', {{
        onsuccess:function(ret, evt) {{
          if (ret.code) {{
            p5.hypereval_output.getDoc().setValue(ret.code);
          }}
        }}
      }});
    }},
    'Alt-S':function(cm) {{p5.$('hypereval-save-button').raise('onclick');}},
    'Alt-L':function(cm) {{p5.$('hypereval-manage-snippets-button').raise('onclick');}},
    'Alt-V':function(cm) {{p5.$('hypereval-output-button').raise('onclick');}},
    'Alt-M':function(cm) {{p5.$('hypereval-manage-snippets-button').raise('onclick');}},
    'Alt-T':function(cm) {{p5.$('hypereval-delete-snippet').raise('onclick');}},
    'Alt-P':function(cm) {{p5.$('hypereval-preview-snippet').raise('onclick');}}
  }}
}});
p5.hypereval_input.on('change',function (cMirror) {{
  p5.$('hypereval-input').el.value = cMirror.getValue();
}});
p5.hypereval_input.setSize('100%', '600px');"
          :x:/@p5.io.unroll-path?value

      widgets
        literal:hypereval-input
          element:textarea
          class:fill
          rows:20
          events

            /*
             * Returns the current code.
             */
            hypereval.widgets.eval.get-code

              /*
               * Retrieving textarea's content to caller.
               */
              get-widget-property:x:/../*/_event?value
                innerValue:x:/..
              return:x:/@get-widget-property/*/*?value

            /*
             * Sets the current code to the given [_arg].
             */
            hypereval.widgets.eval.set-code

              /*
               * Retrieving textarea's content to caller.
               */
              set-widget-property:x:/../*/_event?value
                innerValue:x:/../*/_arg?value
              p5.web.send-javascript:@"p5.hypereval_input.getDoc().setValue(`{0}`);"
                :x:/../*/_arg?value

          oninit

            /*
             * Setting focus initially to "input textarea".
             */
            micro.page.set-focus:x:/../*/_event?value

            /*
             * Checking if we have some code in our session.
             */
            p5.web.session.get:hypereval.code
            if:x:/@p5.web.session.get/*?value
              set-widget-property:hypereval-input
                innerValue:x:/@p5.web.session.get/*?value

        div
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets

                /*
                 * Evaluate Hyperlambda button.
                 */
                button:hypereval-evaluate-button
                  innerValue:@"<span class=""icon-flash""></span>"
                  title:Evaluates the Hyperlambda in your editor - Alt+E
                  style:"width:100px;"
                  onclick:@"p5.$('hypereval-evaluate-button').raise('.onclick',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_output.getDoc().setValue(ret.code);}}});return false;"
                  .onupload

                    /*
                     * Invoked after uploading of file has occurred, at which point our 
                     * session is supposed to contain the file's content.
                     *
                     * Hence, first we retrieve session value, before we return it to
                     * client, which updates the value of our "input CodeMirror" widget.
                     */
                    p5.web.session.get:hypereval.uploaded-file
                    p5.web.return-response-object:code
                      src:x:/@p5.web.session.get/*?value

                    /*
                     * Deleting session object.
                     */
                    p5.web.session.set:hypereval.uploaded-file

                    /*
                     * Checking if this was a zip file, or a Hyperlambda file.
                     * If zip file, we inform user that it was imported, otherwise
                     * we give user feedback about that he'll have to evaluate or execute it.
                     */
                    p5.web.session.get:hypereval.file-type
                    if:x:/-/*?value
                      =:zip

                      /*
                       * Giving user some feedback.
                       */
                      p5.web.session.get:hypereval.snippet-import-count
                      micro.windows.info:{0} snippets successfully imported
                        :x:/@p5.web.session.get/*?value
                        class:micro-windows-info success

                  .onclick

                    /*
                     * Retrieving Hyperlambda and evaluating it.
                     */
                    .exe:node:"exe"
                    get-widget-property:hypereval-input
                      innerValue
                    hyper2lambda:x:/@get-widget-property/*/*?value
                    set:x:/@.exe/#/*
                    add:x:/@.exe/#
                      src:x:/@hyper2lambda/*
                    eval-mutable:x:/@.exe/#

                    /*
                     * Then converting lambda result to Hyperlambda, and returning back to client as JSON
                     */
                    lambda2hyper:x:/@.exe/#/*
                    get-widget-property:hypereval-output-wrapper
                      visible
                    if:x:/@get-widget-property/*/*?value
                      p5.web.return-response-object:code
                        src:x:/@lambda2hyper?value

                    /*
                     * Storing code in session, to make it more easily available.
                     */
                    p5.web.session.set:hypereval.code
                      src:x:/../*/get-widget-property/[0,1]/*/*?value

                /*
                 * Load snippet button.
                 */
                button:hypereval-manage-snippets-button
                  innerValue:@"<span class=""icon-folder-open""></span>"
                  title:Load snippet - Alt+L
                  events

                    /*
                     * Helper event to programatically open load snippet window.
                     */
                    hypereval.widgets.eval.show-load-window

                      /*
                       * To make it simple, we simply "click" button programmatically.
                       */
                      p5.web.widgets.ajax-events.raise:x:/../*/_event?value
                        onclick

                    /*
                     * Helper event to programatically close load snippet window.
                     */
                    hypereval.widgets.eval.close-load-window

                      /*
                       * Closing modal window, if it exists.
                       */
                      if
                        fetch:x:/0/0?value
                          widget-exists:hypereval-manage-snippets
                        delete-widget:hypereval-manage-snippets

                  onclick

                    /*
                     * Shows a modal widget with each snippet from database, allowing
                     * the user to either mark it as a "startup snippet" or as a "page".
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-manage-snippets
                        widgets
                          h3
                            innerValue:Your snippets
                          micro.widgets.grid:hypereval-snippets-grid
                            class:hover
                            events

                              /*
                               * Databinds snippets grid.
                               */
                              hypereval.snippets.databind

                                /*
                                 * Connecting to database, selecting all snippets, 
                                 * and creating one grid item for each record.
                                 */
                                p5.mysql.connect:[hypereval]
                                  p5.mysql.select:@"select * from snippets order by id desc"

                                  /*
                                   * Necessary to find the relative URL of module.
                                   */
                                  p5.io.unroll-path:@HYPEREVAL/
                                  split:x:/@p5.io.unroll-path?value
                                    =:/

                                  /*
                                   * Looping through each snippet, creating one datagrid item
                                   * for each.
                                   */
                                  for-each:x:/@p5.mysql.select/*
                                    p5.html.url-encode:x:/@_dp/#/*/name?value

                                    /*
                                     * Adding "name" cell.
                                     */
                                    if:x:/@_dp/#/*/type?value

                                      /*
                                       * Snippet has a type declaration (either a "page" or a "startup" type of snippet).
                                       */
                                      eval-x:x:/+/*/*/*
                                      add:x:/../*/micro.widgets.grid.databind
                                        src
                                          item
                                            name:{0} ({1})
                                              :x:/@_dp/#/*/name?value
                                              :x:/@_dp/#/*/type?value

                                    else

                                      /*
                                       * Plain snippet.
                                       */
                                      eval-x:x:/+/*/*/*
                                      add:x:/../*/micro.widgets.grid.databind
                                        src
                                          item
                                            name:x:/@_dp/#/*/name?value

                                    /*
                                     * Adding "load" and "delete" cells.
                                     */
                                    eval-x:x:/+/**/.name
                                    add:x:/../*/micro.widgets.grid.databind/0/-
                                      src
                                        load
                                          style:"width:5px;"
                                          widgets
                                            button
                                              innerValue:@"<span class=""icon-folder-open"" style=""width:100%;height:100%;display:inline-block;""></span>"
                                              style:"margin-bottom:0;"
                                              title:Loads this snippet
                                              onclick:@"p5.$(event.target.tagName.toLowerCase() == 'button' ? event.target.id : event.target.parentElement.id).raise('.onclick',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_input.getDoc().setValue(ret.code);}}});return false;"
                                              .onclick

                                                /*
                                                 * Forward evaluated above.
                                                 */
                                                .name:x:/@_dp/#/*/name?value
                                                p5.mysql.connect:[hypereval]
                                                  p5.mysql.select:@"select content, name from snippets where name = @name"
                                                    @name:x:/@.name?value
                                                  p5.web.return-response-object:code
                                                    src:x:/@p5.mysql.select/*/*/content?value
                                                  p5.web.session.set:hypereval.snippet-name
                                                    src:x:/@p5.mysql.select/*/*/name?value

                                                /*
                                                 * Closing modal window.
                                                 */
                                                delete-widget:hypereval-manage-snippets

                                                /*
                                                 * Making sure our Hypereval remembers that this is our
                                                 * "current code".
                                                 */
                                                p5.web.session.set:hypereval.code
                                                  src:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/content?value

                                                /*
                                                 * Signaling that current snippet was changed.
                                                 */
                                                hypereval.snippet.changed

                                        delete
                                          style:"width:5px;"
                                          widgets
                                            button
                                              innerValue:@"<span class=""icon-bin""></span>"
                                              style:"margin-bottom:0;"
                                              title:Deletes this snippet, notice this action is permanent
                                              onclick

                                                /*
                                                 * Forward evaluated above.
                                                 */
                                                .name:x:/@_dp/#/*/name?value

                                                /*
                                                 * Deletes table row, and snippet.
                                                 */
                                                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                                                  element:tr
                                                delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                                                p5.mysql.connect:[hypereval]
                                                  p5.mysql.delete:@"delete from snippets where name = @name"
                                                    @name:x:/@.name?value

                                                /*
                                                 * In case user deleted the "current snippet", we post the
                                                 * changed event, to make sure UI is updated.
                                                 */
                                                hypereval.snippet.changed

                                /*
                                 * Databinds snippets.
                                 */
                                micro.widgets.grid.databind:hypereval-snippets-grid

                            oninit

                              /*
                               * Initial databinding of snippets grid.
                               */
                              hypereval.snippets.databind
                          div
                            class:right
                            widgets
                              div
                                class:strip
                                style:"display:inline-block;"
                                widgets
                                  button
                                    innerValue:Export
                                    style:"margin-bottom:0;"
                                    title:Exports all your snippets to a zip file
                                    onclick

                                      /*
                                       * Exports all snippets to a zip file.
                                       *
                                       * First we make sure our temp folder exists, and
                                       * if it does, we empty it for any previous files.
                                       */
                                      if
                                        fetch:x:/0/0?value
                                          folder-exists:~/temp/hypereval/
                                        not
                                        create-folder:~/temp/hypereval/
                                      list-files:~/temp/hypereval/
                                      delete-file:x:/-/*?name

                                      /*
                                       * Then we select all snippets, and create on Hyperlambda file
                                       * for each, and save to our temporary folder.
                                       */
                                      p5.mysql.connect:[hypereval]
                                        p5.mysql.select:@"select * from snippets"
                                        for-each:x:/@p5.mysql.select/*
                                          _type
                                          if:x:/@_dp/#/*/type?value
                                            set:x:/@_type?value
                                              src:.{0}
                                                :x:/@_dp/#/*/type?value
                                          save-file:~/temp/hypereval/{0}{1}.hl
                                            :x:/@_dp/#/*/name?value
                                            :x:/@_type?value
                                            src:x:/@_dp/#/*/content?value

                                      /*
                                       * Creating a zip file of entire folder, and downloading
                                       * it to client.
                                       */
                                      if
                                        fetch:x:/0/0?value
                                          file-exists:~/temp/hypereval-export.zip
                                        delete-file:~/temp/hypereval-export.zip
                                      zip:~/temp/hypereval-export.zip
                                        src:~/temp/hypereval/
                                      micro.download.file:~/temp/hypereval-export.zip
                                        Content-Type:application/zip
                                        Content-Disposition:@"attachment; filename=""hypereval-export.zip"""

                                  button
                                    innerValue:Close
                                    style:"margin-bottom:0;"
                                    title:Close window
                                    onclick

                                      /*
                                       * Simply deletes the modal widget.
                                       */
                                      delete-widget:hypereval-manage-snippets


                /*
                 * Save Hyperlambda button.
                 */
                button:hypereval-save-button
                  innerValue:@"<span class=""icon-floppy-disk""></span>"
                  title:Saves your current Hyperlambda to your snippets collection - Alt+S
                  onclick

                    /*
                     * Checking if we should provide a default alternative value for "type" of snippet
                     * radio buttons.
                     */
                    p5.web.session.get:hypereval.snippet-name
                    if:x:/@p5.web.session.get/*?value
                      p5.mysql.connect:[hypereval]
                        p5.mysql.scalar:@"select type from snippets where name = @name"
                          @name:x:/@p5.web.session.get/*?value
                        if:x:/@p5.mysql.scalar?value
                          add:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/={0}
                            :x:/@p5.mysql.scalar?value
                            src:checked

                    /*
                     * Making sure we check of "Snippet" radio button, if no other radio buttons have been checked
                     * by above logic.
                     */
                    if:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/checked
                      not
                      add:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/=snippet
                        src:checked

                    /*
                     * Saving Hyperlambda to database, by allowing user to associate a name with snippet,
                     * through a modal window, for then to save the code with the specified name.
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-save-modal
                        widgets
                          h3
                            innerValue:Supply a name please
                          p
                            innerValue:And choose if you want to save your snippet as a page or a startup snippet.
                          micro.widgets.wizard-form:hypereval-save-form
                            text
                              info:Name
                              .data-field:name
                              onkeydown:@"if (event.keyCode == 13) {p5.$('hypereval-save-modal-button').raise('onclick');return false;}"
                              oninit

                                /*
                                 * Setting initial focus to "Name" textbox.
                                 */
                                micro.page.set-focus:x:/../*/_event?value

                                /*
                                 * Checking if we have a snippet name in session, and
                                 * if so, we make sure we default our name to that value.
                                 */
                                p5.web.session.get:hypereval.snippet-name
                                if:x:/-/*?value
                                  set-widget-property:x:/../*/_event?value
                                    value:x:/@p5.web.session.get/*?value

                            radio-group
                              .data-field:type
                              options
                                Snippet:snippet
                                Page:page
                                Startup:startup

                            div
                              class:right
                              style:"margin-top:-1rem;"
                              widgets
                                div
                                  class:strip
                                  style:"display:inline-block;"
                                  widgets
                                    button:hypereval-save-modal-button
                                      innerValue:Save
                                      style:"margin-bottom:0;"
                                      onclick

                                        /*
                                         * Serializing form, opening database connection,
                                         * deleting snippets with similar name, and saving snippet
                                         * into database.
                                         */
                                        get-widget-property:hypereval-input
                                          innerValue
                                        micro.widgets.wizard-form.value:hypereval-save-form

                                        /*
                                         * Sanity checking name of snippet.
                                         */
                                        match:x:/@micro.widgets.wizard-form.value/*/name?value
                                          src:regex:/^[-a-z0-9_ ]*$/i
                                        if:x:/@match/*?name
                                          not
                                          micro.windows.info:Only use spaces, a-z, 0-9 and '-' in your name
                                            class:micro-windows-info warning
                                          return

                                        /*
                                         * Making sure we insert page as correct type.
                                         */
                                        _type
                                        if:x:/@micro.widgets.wizard-form.value/*/type?value
                                          !=:snippet
                                          set:x:/@_type?value
                                            src:x:/@micro.widgets.wizard-form.value/*/type?value

                                        /*
                                         * Inserting snippet into database.
                                         */
                                        p5.mysql.connect:[hypereval]
                                          p5.mysql.delete:@"delete from snippets where name = @name"
                                            @name:x:/@micro.widgets.wizard-form.value/*/name?value
                                          p5.mysql.insert:@"insert into snippets (name, content, type) values (@name, @content, @type)"
                                            @name:x:/@micro.widgets.wizard-form.value/*/name?value
                                            @content:x:/@get-widget-property/*/*?value
                                            @type::x:/@_type?value

                                        /*
                                         * Closing modal window, and giving user some feedback.
                                         */
                                        delete-widget:hypereval-save-modal
                                        micro.windows.info:Snippet was successfully saved
                                          class:micro-windows-info success

                                        /*
                                         * Making sure we store name used into session, in case
                                         * user wants to save the same snippet again.
                                         */
                                        p5.web.session.set:hypereval.snippet-name
                                          src:x:/@micro.widgets.wizard-form.value/*/name?value

                                        /*
                                         * Storing code in session, to make it more easily available.
                                         */
                                        p5.web.session.set:hypereval.code
                                          src:x:/../*/get-widget-property/*/*?value

                                        /*
                                         * Signaling that snippet was changed.
                                         */
                                        hypereval.snippet.changed

                                    button
                                      innerValue:Close
                                      style:"margin-bottom:0;"
                                      onclick

                                        /*
                                         * Simply closing modal window.
                                         */
                                        delete-widget:hypereval-save-modal

                /*
                 * Preview button.
                 */
                button:hypereval-preview-snippet
                  innerValue:@"<span class=""icon-play3""></span>"
                  title:Previews your page - Alt+P
                  disabled
                  oninit

                    /*
                     * Initially posting the "snippet changed" event, to make sure
                     * we can preview snippet, if possible.
                     */
                    hypereval.snippet.changed

                  events

                    /*
                     * Invoked when "current snippet" has been changed, either loaded
                     * from database, or saved to database.
                     */
                    hypereval.snippet.changed

                      /*
                       * Checking if this is a page, and if so, enabling button.
                       */
                      p5.web.session.get:hypereval.snippet-name
                      p5.mysql.connect:[hypereval]
                        p5.mysql.scalar:"select type from snippets where name = @name"
                          @name:x:/@p5.web.session.get/*?value
                        if:x:/@p5.mysql.scalar?value
                          =:page
                          delete-widget-property:x:/../*/_event?value
                            disabled
                        else
                          set-widget-property:x:/../*/_event?value
                            disabled

                  onclick

                    /*
                     * Previewing "current snippet" in a new window.
                     */
                    p5.web.session.get:hypereval.snippet-name
                    p5.mysql.connect:[hypereval]
                      p5.mysql.scalar:"select name from snippets where name = @name and type = 'page'"
                        @name:x:/@p5.web.session.get/*?value
                      if:x:/-?value
                        not
                        return
                      p5.html.url-encode:x:/@p5.mysql.scalar?value
                      p5.web.send-javascript:@"window.open('/hypereval?name={0}');"
                        :x:/@p5.html.url-encode?value

                /*
                 * View "Output" button.
                 */
                button:hypereval-output-button
                  innerValue:@"<span class=""icon-eye""></span>"
                  title:View the output code, which results from evaluating your code - Alt+V
                  onclick

                    /*
                     * Toggles visibility of output.
                     */
                    get-widget-property:hypereval-output-wrapper
                      visible
                    if:x:/@get-widget-property/*/*?value

                      /*
                       * Hiding "output" widget.
                       */
                      set-widget-property:hypereval-output-wrapper
                        visible:false
                      micro.css.delete:x:/../*/_event?value
                        class:toggled

                    else

                      /*
                       * Showing "output" widget.
                       */
                      set-widget-property:hypereval-output-wrapper
                        visible:true
                      micro.css.add:x:/../*/_event?value
                        class:toggled

                /*
                 * Download Hyperlambda button.
                 */
                button:hypereval-download-button
                  innerValue:@"<span class=""icon-download2""></span>"
                  title:Downloads the Hyperlambda in your editor to your client
                  onclick

                    /*
                     * Creates a temporary file containing Hyperlambda, 
                     * and allows user to download it to his client.
                     */
                    .defaults
                      hypereval.snippet-name:hypereval
                      type
                    p5.web.session.get:hypereval.snippet-name
                    if:x:/@p5.web.session.get/*?value
                      p5.mysql.connect:[hypereval]
                        p5.mysql.scalar:@"select type from snippets where name = @name"
                          @name:x:/@p5.web.session.get/*?value
                        if:x:/@p5.mysql.scalar?value
                          set:x:/@.defaults/*/type?value
                            src:.{0}
                              :x:/@p5.mysql.scalar?value
                    get-widget-property:hypereval-input
                      innerValue
                    save-file:~/temp/hypereval-code.hl
                      src:x:/@get-widget-property/*/*?value
                    eval-x:x:/+/*/Content-Disposition
                    micro.download.file:~/temp/hypereval-code.hl
                      Content-Type:x-application/hyperlambda
                      Content-Disposition:@"attachment; filename=""{0}{1}.hl"""
                        :x:(/@p5.web.session.get/*/hypereval.snippet-name|/@.defaults/*/hypereval.snippet-name)/$?value
                        :x:/@.defaults/*/type?value

                /*
                 * Upload Hyperlambda button.
                 */
                button
                  innerValue:@"<span class=""icon-upload2""></span>"
                  title:Uploads a Hyperlambda file from your client
                  onclick:@"p5.dropzone.browse();event.stopPropagation(true);return false;"

        div:hypereval-output-wrapper
          visible:false
          style:"clear:both;"
          widgets
            literal:hypereval-output
              element:textarea
              class:fill
              rows:10
              oninit

                /*
                 * Transforming output textarea to CodeMirror instance.
                 */
                p5.io.unroll-path:@HYPEREVAL/
                p5.web.send-javascript:@"p5.hypereval_output = CodeMirror.fromTextArea(p5.$('hypereval-output').el, {{
mode:'hyperlambda',
theme:'hyperlambda',
lineNumbers:true,
styleActiveLine:true,
path:'{0}/media/codemirror/',
tabSize:2,
indentAuto:true,
readOnly:true
}});"
                  :x:/@p5.io.unroll-path?value
