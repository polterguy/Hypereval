
/*
 * Responsible for creating the CodeMirror Hyperlambda evaluator widget.
 */
create-event:hypereval.widgets.eval

  /*
   * Used to separate arguments from the rest of our lambda.
   */
  .signal

  /*
   * Decorating widget with arguments.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--(!/_arg)

  /*
   * Returning widget to caller.
   */
  return
    container
      style:"margin-bottom:20px;"
      oninit

        /*
         * Making sure our page becomes a "dropzone" to allow user to upload 
         * a Hyperlambda file (which will populate the CodeMirror module with its code).
         */
        p5.io.unroll-path:@HYPEREVAL/
        split:x:/@p5.io.unroll-path?value
          =:/
        eval-x:x:/+/*/url
        micro.page.create-dropzone
          filter:hl
          multiple:false
          url:{0}/upload
            :x:/@split/0/-?name
          .onfinish:@"function(){p5.$('hypereval-evaluate-button').raise('.onupload',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_input.getDoc().setValue(ret.code);}}});}"

        /*
         * Including CodeMirror JavaScript.
         */
        p5.web.include-javascript-file
          @HYPEREVAL/media/codemirror/lib/codemirror.js
          @HYPEREVAL/media/codemirror/addon/selection/active-line.js
          @HYPEREVAL/media/codemirror/addon/display/fullscreen.js
          @HYPEREVAL/media/codemirror/addon/hint/show-hint.js
          @HYPEREVAL/media/codemirror/mode/hyperlambda/hyperlambda.js

        /*
         * Making sure necessary Stylesheet files are included
         */
        p5.web.include-css-file
          @HYPEREVAL/media/codemirror/lib/codemirror.css
          @HYPEREVAL/media/codemirror/theme/hyperlambda.css
          @HYPEREVAL/media/codemirror/addon/hint/show-hint.css
          @HYPEREVAL/media/codemirror/addon/display/fullscreen.css

        /*
         * Including Active Events JSON on page.
         */
        vocabulary
        join:x:/@vocabulary/*?value
          sep:,
          wrap:'
        p5.web.include-javascript:@"CodeMirror._hyperlispKeywords = [{0}];"
          :x:/@join?value

        /*
         * Transforming input textarea to CodeMirror instance.
         */
        p5.io.unroll-path:@HYPEREVAL/
        p5.web.send-javascript:@"p5.hypereval_input = CodeMirror.fromTextArea(p5.$('hypereval-input').el, {{
  mode:'hyperlambda',
  theme:'hyperlambda',
  lineNumbers:true,
  styleActiveLine:true,
  path:'{0}/media/codemirror/',
  autofocus:true,
  tabSize:2,
  indentAuto:true,
  extraKeys: {{
    'Ctrl-Space':'autocomplete',
    'Shift-Tab':'indentLess',
    'Tab':'indentMore',
    'Alt-F':function(cm) {{cm.setOption('fullScreen', !cm.getOption('fullScreen'));}},
    'Alt-E':function(cm) {{
      p5.$('hypereval-evaluate-button').raise('.onclick', {{
        onsuccess:function(ret, evt) {{
          if (ret.code) {{
            p5.hypereval_output.getDoc().setValue(ret.code);
          }}
        }}
      }});
    }},
    'Alt-S':function(cm) {{p5.$('hypereval-save-button').raise('onclick');}},
    'Alt-L':function(cm) {{p5.$('hypereval-load-button').raise('onclick');}},
    'Alt-V':function(cm) {{p5.$('hypereval-output-button').raise('onclick');}},
    'Alt-M':function(cm) {{p5.$('hypereval-manage-snippets-button').raise('onclick');}},
    'Alt-D':function(cm) {{p5.$('hypereval-discuss-code').raise('onclick');}},
    'Alt-T':function(cm) {{p5.$('hypereval-delete-snippet').raise('onclick');}}
  }}
}});
p5.hypereval_input.on('change',function (cMirror) {{
  p5.$('hypereval-input').el.value = cMirror.getValue();
}});
p5.hypereval_input.setSize('100%', '600px');"
          :x:/@p5.io.unroll-path?value

      widgets
        literal:hypereval-input
          element:textarea
          class:fill
          rows:20
          oninit

            /*
             * Setting focus initially to "input textarea".
             */
            micro.page.set-focus:x:/../*/_event?value

            /*
             * Checking if we have some code in our session.
             */
            p5.web.session.get:hypereval.code
            if:x:/@p5.web.session.get/*?value
              set-widget-property:hypereval-input
                innerValue:x:/@p5.web.session.get/*?value

        div
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets

                /*
                 * Evaluate Hyperlambda button.
                 */
                button:hypereval-evaluate-button
                  innerValue:@"<span class=""icon-flash""></span>"
                  title:Evaluates the Hyperlambda in your editor - Alt+E
                  style:"width:100px;"
                  onclick:@"p5.$('hypereval-evaluate-button').raise('.onclick',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_output.getDoc().setValue(ret.code);}}});return false;"
                  .onupload

                    /*
                     * Invoked after uploading of file has occurred, at which point our 
                     * session is supposed to contain the file's content.
                     *
                     * Hence, first we retrieve session value, before we return it to
                     * client, which updates the value of our "input CodeMirror" widget.
                     */
                    p5.web.session.get:hypereval.uploaded-file
                    p5.web.return-response-object:code
                      src:x:/@p5.web.session.get/*?value

                    /*
                     * Deleting session object.
                     */
                    p5.web.session.set:hypereval.uploaded-file

                  .onclick

                    /*
                     * Retrieving Hyperlambda and evaluating it.
                     */
                    .exe:node:"exe"
                    get-widget-property:hypereval-input
                      innerValue
                    hyper2lambda:x:/@get-widget-property/*/*?value
                    set:x:/@.exe/#/*
                    add:x:/@.exe/#
                      src:x:/@hyper2lambda/*
                    eval-mutable:x:/@.exe/#

                    /*
                     * Then converting lambda result to Hyperlambda, and returning back to client as JSON
                     */
                    lambda2hyper:x:/@.exe/#/*
                    get-widget-property:hypereval-output-wrapper
                      visible
                    if:x:/@get-widget-property/*/*?value
                      p5.web.return-response-object:code
                        src:x:/@lambda2hyper?value

                    /*
                     * Storing code in session, to make it more easily available.
                     */
                    p5.web.session.set:hypereval.code
                      src:x:/../*/get-widget-property/[0,1]/*/*?value

                /*
                 * View "Output" button.
                 */
                button:hypereval-output-button
                  innerValue:@"<span class=""icon-eye""></span>"
                  title:View the output code, which results from evaluating your code - Alt+V
                  onclick

                    /*
                     * Toggles visibility of output.
                     */
                    get-widget-property:hypereval-output-wrapper
                      visible
                    if:x:/@get-widget-property/*/*?value
                      set-widget-property:hypereval-output-wrapper
                        visible:false
                    else
                      set-widget-property:hypereval-output-wrapper
                        visible:true

                /*
                 * Load Hyperlambda button.
                 */
                button:hypereval-load-button
                  innerValue:@"<span class=""icon-folder-open""></span>"
                  title:Loads a snippet from your snippets collection - Alt+L
                  onclick

                    /*
                     * Displaying a modal window to user, to allow him to select 
                     * a Hyperlambda snippet to load.
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-load-modal
                        widgets
                          h3
                            innerValue:Select a snippet
                          micro.widgets.grid:hypereval-snippet-grid
                            class:hover
                            oninit

                              /*
                               * Opening up database collection, selecting all snippets,
                               * and populating grid.
                               */
                              p5.mysql.connect:[hypereval]
                                p5.mysql.select:@"select name from snippets order by id desc"

                                /*
                                 * Looping through each snippet, creating one grid item for each.
                                 */
                                for-each:x:/@p5.mysql.select/*/*?value
                                  eval-x:x:/+/*/*/*|/+/*/*/*/*/.name
                                  add:x:/..p5.mysql.connect/*/micro.widgets.grid.databind
                                    src
                                      item
                                        .row
                                          .name:x:/@_dp?value
                                          role:button
                                          onclick:@"p5.$(event.target.parentElement.id).raise('.onclick',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_input.getDoc().setValue(ret.code);}}});return false;"
                                          .onclick

                                            /*
                                             * Retrieves name of snippet clicked, loading it from database, populating
                                             * editor, and closing modal widget.
                                             */
                                            get-widget-property:x:/../*/_event?value
                                              .name
                                            p5.mysql.connect:[hypereval]
                                              p5.mysql.select:@"select content, name from snippets where name = @name"
                                                @name:x:/@get-widget-property/*/*?value
                                              p5.web.return-response-object:code
                                                src:x:/@p5.mysql.select/*/*/content?value
                                              p5.web.session.set:hypereval.snippet-name
                                                src:x:/@p5.mysql.select/*/*/name?value

                                            /*
                                             * Closing modal window.
                                             */
                                            delete-widget:hypereval-load-modal

                                            /*
                                             * Making sure our Hypereval remembers that this is our
                                             * "current code".
                                             */
                                            p5.web.session.set:hypereval.code
                                              src:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/content?value

                                        name:x:/@_dp?value

                                /*
                                 * Databinding grid.
                                 */
                                micro.widgets.grid.databind:hypereval-snippet-grid

                /*
                 * Save Hyperlambda button.
                 */
                button:hypereval-save-button
                  innerValue:@"<span class=""icon-floppy-o""></span>"
                  title:Saves your current Hyperlambda to your snippets collection - Alt+S
                  onclick

                    /*
                     * Saving Hyperlambda to database, by allowing user to associate a name with snippet,
                     * through a modal window, for then to save the code with the specified name.
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-save-modal
                        widgets
                          h3
                            innerValue:Supply a name please
                          p
                            innerValue:Notice, if you supply a name that already exists in your snippet collection, the old snippet will be deleted.
                          micro.widgets.wizard-form:hypereval-save-form
                            text
                              info:Name
                              .data-field:name
                              onkeydown:@"if (event.keyCode == 13) {p5.$('hypereval-save-modal-button').raise('onclick');return false;}"
                              oninit

                                /*
                                 * Setting initial focus to "Name" textbox.
                                 */
                                micro.page.set-focus:x:/../*/_event?value

                                /*
                                 * Checking if we have a snippet name in session, and
                                 * if so, we make sure we default our name to that value.
                                 */
                                p5.web.session.get:hypereval.snippet-name
                                if:x:/-/*?value
                                  set-widget-property:x:/../*/_event?value
                                    value:x:/@p5.web.session.get/*?value

                            div
                              class:right
                              widgets
                                button:hypereval-save-modal-button
                                  innerValue:Save
                                  style:"margin-bottom:0;"
                                  onclick

                                    /*
                                     * Serializing form, opening database connection,
                                     * deleting snippets with similar name, and saving snippet
                                     * into database.
                                     */
                                    get-widget-property:hypereval-input
                                      innerValue
                                    micro.widgets.wizard-form.value:hypereval-save-form
                                    p5.mysql.connect:[hypereval]
                                      p5.mysql.scalar:@"select type from snippets where name = @name"
                                        @name:x:/@micro.widgets.wizard-form.value/*/name?value
                                      p5.mysql.delete:@"delete from snippets where name = @name"
                                        @name:x:/@micro.widgets.wizard-form.value/*/name?value
                                      p5.mysql.insert:@"insert into snippets (name, content, type) values (@name, @content, @type)"
                                        @name:x:/@micro.widgets.wizard-form.value/*/name?value
                                        @content:x:/@get-widget-property/*/*?value
                                        @type::x:/@p5.mysql.scalar?value

                                    /*
                                     * Closing modal window, and giving user some feedback.
                                     */
                                    delete-widget:hypereval-save-modal
                                    micro.windows.info:Snippet was successfully saved
                                      class:micro-windows-info success

                                    /*
                                     * Making sure we store name used into session, in case
                                     * user wants to save the same snippet again.
                                     */
                                    p5.web.session.set:hypereval.snippet-name
                                      src:x:/@micro.widgets.wizard-form.value/*/name?value

                                    /*
                                     * Storing code in session, to make it more easily available.
                                     */
                                    p5.web.session.set:hypereval.code
                                      src:x:/../*/get-widget-property/*/*?value

                /*
                 * Download Hyperlambda button.
                 */
                button:hypereval-download-button
                  innerValue:@"<span class=""icon-download""></span>"
                  title:Downloads the Hyperlambda in your editor to your client
                  onclick

                    /*
                     * Creates a temporary file containing Hyperlambda, 
                     * and allows user to download it to his client.
                     */
                    .defaults
                      hypereval.snippet-name:hypereval
                    p5.web.session.get:hypereval.snippet-name
                    get-widget-property:hypereval-input
                      innerValue
                    save-file:~/temp/hypereval-code.hl
                      src:x:/@get-widget-property/*/*?value
                    eval-x:x:/+/*/Content-Disposition
                    micro.download.file:~/temp/hypereval-code.hl
                      Content-Type:x-application/hyperlambda
                      Content-Disposition:@"attachment; filename=""{0}.hl"""
                        :x:(/@p5.web.session.get/*/hypereval.snippet-name|/@.defaults/*/hypereval.snippet-name)/$?value

                /*
                 * Upload Hyperlambda button.
                 */
                button
                  innerValue:@"<span class=""icon-upload""></span>"
                  title:Uploads a Hyperlambda file from your client
                  onclick:@"p5.dropzone.browse();event.stopPropagation(true);return false;"

                /*
                 * Manage "pages" button.
                 */
                button:hypereval-manage-snippets-button
                  innerValue:@"<span class=""icon-th-large""></span>"
                  title:Manage snippets - Alt+M
                  onclick

                    /*
                     * Shows a modal widget with each snippet from database, allowing
                     * the user to either mark it as a "startup snippet" or as a "page".
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-manage-snippets
                        widgets
                          h3
                            innerValue:Manage snippets
                          micro.widgets.grid:hypereval-snippets-grid
                            class:striped
                            events

                              /*
                               * Databinds snippets grid.
                               */
                              hypereval.snippets.databind

                                /*
                                 * Connecting to database, selecting all snippets, 
                                 * and creating one grid item for each record.
                                 */
                                p5.mysql.connect:[hypereval]
                                  p5.mysql.select:@"select * from snippets order by id desc"

                                  /*
                                   * Necessary to find the relative URL of module.
                                   */
                                  p5.io.unroll-path:@HYPEREVAL/
                                  split:x:/@p5.io.unroll-path?value
                                    =:/

                                  /*
                                   * Looping through each snippet, creating one datagrid item
                                   * for each.
                                   */
                                  for-each:x:/@p5.mysql.select/*
                                    p5.html.url-encode:x:/@_dp/#/*/name?value
                                    eval-x:x:/+/*/*/*

                                    /*
                                     * Adding "name" cell.
                                     */
                                    add:x:/../*/micro.widgets.grid.databind
                                      src
                                        item
                                          name:x:/@_dp/#/*/name?value

                                    /*
                                     * Adding "radio buttons" cell and "preview page" cell.
                                     *
                                     * Notice, this first part simply adds to our [add] invocation below,
                                     * according to the snippet's status.
                                     */
                                    add:x:/+3/*/*/*/*/*/widgets/*/input/*(/.data-field/={0}|/.data-field/=default)/[0,1]/.
                                      :x:/@_dp/#/*/type?value
                                      src
                                        checked
                                    eval-x:x:/+2/*/*/*/*/*/widgets/*/input/*/name|/+2/**(/.id|/href|/a)
                                    if:x:/@_dp/#/*/type?value
                                      !=:page
                                      add:x:/./+/**/a
                                        src
                                          visible:false

                                    /*
                                     * This is where we actually add our "radio button" cells, and
                                     * our "View page" cell.
                                     */
                                    add:x:/../*/micro.widgets.grid.databind/0/-
                                      src
                                        type
                                          widgets
                                            label
                                              style:"margin-bottom:0;"
                                              widgets
                                                input
                                                  type:radio
                                                  .data-field:default
                                                  style:"margin-bottom:0;"
                                                  name:item-{0}
                                                    :x:/@_dp/#/*/id?value
                                                  onchange
                                                    .id:x:/@_dp/#/*/id?value

                                                    /*
                                                     * Updating "type" of snippet, and setting
                                                     * "View page"'s hyperlink to visible/invisible.
                                                     */
                                                    p5.mysql.connect:[hypereval]
                                                      p5.mysql.update:@"update snippets set type = null where id = @id"
                                                        @id:x:/@.id?value
                                                    set-widget-property:hypereval-view-page-{0}
                                                      :x:/@.id?value
                                                      visible:false

                                                span
                                                  innerValue:Snippet
                                            label
                                              style:"margin-bottom:0;"
                                              widgets
                                                input
                                                  type:radio
                                                  .data-field:page
                                                  style:"margin-bottom:0;margin-left:5px;"
                                                  name:item-{0}
                                                    :x:/@_dp/#/*/id?value
                                                  onchange

                                                    /*
                                                     * Updating "type" of snippet, and setting
                                                     * "View page"'s hyperlink to visible/invisible.
                                                     */
                                                    .id:x:/@_dp/#/*/id?value
                                                    p5.mysql.connect:[hypereval]
                                                      p5.mysql.update:@"update snippets set type = 'page' where id = @id"
                                                        @id:x:/@.id?value
                                                    set-widget-property:hypereval-view-page-{0}
                                                      :x:/@.id?value
                                                      visible:true

                                                span
                                                  innerValue:Page
                                            label
                                              style:"margin-bottom:0;"
                                              widgets
                                                input
                                                  type:radio
                                                  .data-field:startup
                                                  style:"margin-bottom:0;margin-left:5px;"
                                                  name:item-{0}
                                                    :x:/@_dp/#/*/id?value
                                                  onchange

                                                    /*
                                                     * Updating "type" of snippet, and setting
                                                     * "View page"'s hyperlink to visible/invisible.
                                                     */
                                                    .id:x:/@_dp/#/*/id?value
                                                    p5.mysql.connect:[hypereval]
                                                      p5.mysql.update:@"update snippets set type = 'startup' where id = @id"
                                                        @id:x:/@.id?value
                                                    set-widget-property:hypereval-view-page-{0}
                                                      :x:/@.id?value
                                                      visible:false

                                                span
                                                  innerValue:Startup
                                        type
                                          widgets
                                            a:hypereval-view-page-{0}
                                              :x:/@_dp/#/*/id?value
                                              target:_blank
                                              href:/{0}?name={1}
                                                :x:/@split/0/-?name
                                                :x:/@p5.html.url-encode?value
                                              innerValue:View page ...

                                /*
                                 * Databinds snippets.
                                 */
                                micro.widgets.grid.databind:hypereval-snippets-grid

                            oninit

                              /*
                               * Initial databinding of snippets grid.
                               */
                              hypereval.snippets.databind

                /*
                 * Analyze Hyperlambda snippet button.
                 */
                button:hypereval-discuss-code
                  innerValue:@"<span class=""icon-info-circle""></span>"
                  title:Discuss your code with your computer - Alt+D
                  onclick

                    /*
                     * Evaluates event responsible for initiating an anlyze session of your snippet.
                     */
                    get-widget-property:hypereval-input
                      innerValue
                    hypereval.analyze:x:/@get-widget-property/*/*?value

                /*
                 * Delete Hyperlambda snippet button.
                 */
                button:hypereval-delete-snippet
                  innerValue:@"<span class=""icon-trash""></span>"
                  title:Deletes the currently active snippet, if any - Alt+T
                  onclick

                    /*
                     * Retrieving snippet's name.
                     */
                    p5.web.session.get:hypereval.snippet-name
                    if:x:/@p5.web.session.get/*?value
                      not

                      /*
                       * No active snippet, warning use.
                       */
                      micro.windows.info:No active snippet
                        class:micro-windows-info
                      return

                    /*
                     * Asking user to confirm deletion.
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-confirm-deletion
                        widgets
                          h3
                            innerValue:Please confirm deletion
                          p
                            innerValue:Are you sure you wish to delete this snippet? This action is permanent.
                          div
                            class:right
                            widgets
                              div
                                class:strip
                                style:"display:inline-block;"
                                widgets
                                  button:hypereval-confirm-deletion-button
                                    innerValue:Yes
                                    style:"margin-bottom:0;"
                                    oninit

                                      /*
                                       * Setting initial focus to "Yes" button.
                                       */
                                      micro.page.set-focus:x:/../*/_event?value

                                    onclick:@"p5.$('hypereval-confirm-deletion-button').raise('.onclick',{onsuccess:function(ret, evt){p5.hypereval_input.getDoc().setValue('');}});return false;"
                                    .onclick

                                      /*
                                       * Retrieving snippet's name.
                                       */
                                      p5.web.session.get:hypereval.snippet-name

                                      /*
                                       * Opening up database connection, and deleting snippet.
                                       */
                                      p5.mysql.connect:[hypereval]
                                        p5.mysql.delete:@"delete from snippets where name = @name"
                                          @name:x:/@p5.web.session.get/*?value

                                      /*
                                       * Providing feedback to user, and deleting modal widget.
                                       */
                                      delete-widget:hypereval-confirm-deletion
                                      micro.windows.info:Snippet was successfully deleted
                                        class:micro-windows-info success

                                      /*
                                       * Clearing session for all relevant values.
                                       */
                                      p5.web.session.set:hypereval.uploaded-file
                                      p5.web.session.set:hypereval.snippet-name
                                      p5.web.session.set:hypereval.code

                                  button
                                    innerValue:No
                                    style:"margin-bottom:0;"
                                    onclick

                                      /*
                                       * Simply deleting modal widget.
                                       */
                                      delete-widget:hypereval-confirm-deletion

        div:hypereval-output-wrapper
          visible:false
          style:"clear:both;"
          widgets
            literal:hypereval-output
              element:textarea
              class:fill
              rows:10
              oninit

                /*
                 * Transforming output textarea to CodeMirror instance.
                 */
                p5.io.unroll-path:@HYPEREVAL/
                p5.web.send-javascript:@"p5.hypereval_output = CodeMirror.fromTextArea(p5.$('hypereval-output').el, {{
mode:'hyperlambda',
theme:'hyperlambda',
lineNumbers:true,
styleActiveLine:true,
path:'{0}/media/codemirror/',
tabSize:2,
indentAuto:true,
readOnly:true
}});"
                  :x:/@p5.io.unroll-path?value
