
/*
 * Responsible for creating the CodeMirror Hyperlambda evaluator widget.
 */
create-event:hypereval.widgets.eval

  /*
   * Used to separate arguments from the rest of our lambda.
   */
  .signal

  /*
   * Figuring out font size to use for widget.
   */
  .defaults
    font-size:9.25
    theme:hyperlambda
  select-data:x:/*/*/hypereval.settings

  /*
   * Decorating widget with arguments.
   */
  add:x:/../*/return/*/container
    src:x:/@.signal/--(!/_arg)

  /*
   * Returning widget to caller.
   */
  eval-x:x:/+/**/.theme|/+/*/container/*/style
  return
    container
      style:"font-size:{0}pt !important;"
        :x:(/@select-data/*/*/font-size|/../*/.defaults/*/font-size)/$?value
      events

        /*
         * Helper method to determine if Hypereval is already loaded on page.
         */
        hypereval.widgets.eval.exists
          return:bool:true

        /*
         * Helper method to programmatically load the named snippet.
         *
         * Expects [_arg] to have snippet's name.
         */
        hypereval.widgets.eval.load

          /*
           * Using API method to make it simple.
           */
          hypereval.snippets.get:x:/../*/_arg?value

          /*
           * Modifying textarea's and CodeMirror's values.
           */
          set-widget-property:hypereval-input
            innerValue:x:/@hypereval.snippets.get?value
          p5.web.send-javascript:@"p5.hypereval_input.getDoc().setValue(`{0}`);"
            :x:/@hypereval.snippets.get?value

          /*
           * Storing into session the snippet's name, and its code.
           */
          p5.web.session.set:hypereval.snippet-name
            src:x:/../*/_arg?value
          p5.web.session.set:hypereval.code
            src:x:/@hypereval.snippets.get?value

      oninit

        /*
         * Making sure our page becomes a "dropzone" to allow user to upload 
         * a Hyperlambda file (which will populate the CodeMirror module with its code).
         */
        p5.io.unroll-path:@HYPEREVAL/
        split:x:/@p5.io.unroll-path?value
          =:/
        eval-x:x:/+/*/url
        micro.page.create-dropzone
          filter:hl|zip
          multiple:false
          url:{0}/upload
            :x:/@split/0/-?name
          .onfinish:@"function(){p5.$('hypereval-evaluate-snippet').raise('.onupload',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_input.getDoc().setValue(ret.code);}}});}"

        /*
         * Including CodeMirror JavaScript.
         */
        p5.web.include-javascript-file
          @HYPEREVAL/media/codemirror/lib/codemirror.js
          @HYPEREVAL/media/codemirror/addon/selection/active-line.js
          @HYPEREVAL/media/codemirror/addon/display/fullscreen.js
          @HYPEREVAL/media/codemirror/addon/hint/show-hint.js
          @HYPEREVAL/media/codemirror/mode/hyperlambda/hyperlambda.js

        /*
         * Making sure necessary Stylesheet files are included
         */
        .theme:x:(/@select-data/*/*/theme|/../*/.defaults/*/theme)/$?value
        p5.web.include-css-file
          @HYPEREVAL/media/codemirror/lib/codemirror.css
          @HYPEREVAL/media/codemirror/addon/hint/show-hint.css
          @HYPEREVAL/media/codemirror/addon/display/fullscreen.css
        p5.web.include-css-file:@HYPEREVAL/media/codemirror/theme/{0}.css
          :x:/@.theme?value

        /*
         * Including Active Events JSON on page.
         */
        vocabulary
        join:x:/@vocabulary/*?value
          sep:,
          wrap:'
        p5.web.include-javascript:@"CodeMirror._hyperlispKeywords = [{0}];"
          :x:/@join?value

        /*
         * Transforming input textarea to CodeMirror instance.
         */
        p5.io.unroll-path:@HYPEREVAL/
        p5.web.send-javascript:@"p5.hypereval_input = CodeMirror.fromTextArea(p5.$('hypereval-input').el, {{
  mode:'hyperlambda',
  theme:'{1}',
  lineNumbers:true,
  styleActiveLine:true,
  path:'{0}/media/codemirror/',
  autofocus:true,
  tabSize:2,
  indentAuto:true,
  extraKeys: {{
    'Ctrl-Space':'autocomplete',
    'Shift-Tab':'indentLess',
    'Tab':'indentMore',
    'Alt-F':function(cm) {{cm.setOption('fullScreen', !cm.getOption('fullScreen'));}},
    'Alt-E':function(cm) {{
      p5.$('hypereval-evaluate-snippet').raise('.onclick', {{
        onsuccess:function(ret, evt) {{
          if (ret.code) {{
            p5.hypereval_output.getDoc().setValue(ret.code);
          }}
        }}
      }});
    }},
    'Alt-S':function(cm) {{p5.$('hypereval-save-snippet').raise('onclick');}},
    'Alt-L':function(cm) {{p5.$('hypereval-load-snippet').raise('onclick');}},
    'Alt-V':function(cm) {{p5.$('hypereval-view-output').raise('onclick');}},
    'Alt-D':function(cm) {{p5.$('hypereval-delete-snippet').raise('onclick');}},
    'Alt-N':function(cm) {{p5.$('hypereval-new-snippet').raise('onclick');}},
    'Alt-P':function(cm) {{p5.$('hypereval-preview-snippet').raise('onclick');}}
  }}
}});
p5.hypereval_input.on('change',function (cMirror) {{
  p5.$('hypereval-input').el.value = cMirror.getValue();
}});
p5.hypereval_input.setSize('100%', '600px');"
          :x:/@p5.io.unroll-path?value
          :x:/@.theme?value

      widgets
        literal:hypereval-input
          element:textarea
          class:fill
          rows:20
          events

            /*
             * Returns the current code.
             */
            hypereval.widgets.eval.get-code

              /*
               * Retrieving textarea's content to caller.
               */
              get-widget-property:x:/../*/_event?value
                innerValue:x:/..
              return:x:/@get-widget-property/*/*?value

            /*
             * Sets the current code to the given [_arg].
             */
            hypereval.widgets.eval.set-code

              /*
               * Retrieving textarea's content to caller.
               */
              set-widget-property:x:/../*/_event?value
                innerValue:x:/../*/_arg?value
              p5.web.send-javascript:@"p5.hypereval_input.getDoc().setValue(`{0}`);"
                :x:/../*/_arg?value

          oninit

            /*
             * Setting focus initially to "input textarea".
             */
            micro.page.set-focus:x:/../*/_event?value

            /*
             * Checking if we have some code in our session.
             */
            p5.web.session.get:hypereval.code
            if:x:/@p5.web.session.get/*?value
              set-widget-property:hypereval-input
                innerValue:x:/@p5.web.session.get/*?value

        div
          class:right air-top
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets

                /*
                 * Evaluate Hyperlambda button.
                 */
                button:hypereval-evaluate-snippet
                  innerValue:@"<span class=""icon-flash""></span>"
                  title:Evaluates the Hyperlambda in your editor - Alt+E
                  style:"width:100px;"
                  onclick:@"p5.$('hypereval-evaluate-snippet').raise('.onclick',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_output.getDoc().setValue(ret.code);}}});return false;"
                  .onupload

                    /*
                     * Invoked after uploading of file has occurred, at which point our 
                     * session is supposed to contain the file's content.
                     *
                     * Hence, first we retrieve session value, before we return it to
                     * client, which updates the value of our "input CodeMirror" widget.
                     */
                    p5.web.session.get:hypereval.uploaded-file
                    p5.web.return-response-object:code
                      src:x:/@p5.web.session.get/*?value

                    /*
                     * Deleting session object.
                     */
                    p5.web.session.set:hypereval.uploaded-file

                    /*
                     * Checking if this was a zip file, or a Hyperlambda file.
                     * If zip file, we inform user that it was imported, otherwise
                     * we give user feedback about that he'll have to evaluate or execute it.
                     */
                    p5.web.session.get:hypereval.file-type
                    if:x:/-/*?value
                      =:zip

                      /*
                       * Giving user some feedback.
                       */
                      p5.web.session.get:hypereval.snippet-import-count
                      micro.windows.info:{0} snippets successfully imported
                        :x:/@p5.web.session.get/*?value
                        class:micro-windows-info success

                    else

                      /*
                       * Giving user some feedback.
                       */
                      micro.windows.info:Notice, your snippet was not saved
                        class:micro-windows-info success

                  .onclick

                    /*
                     * Retrieving Hyperlambda and evaluating it.
                     */
                    .exe:node:"exe"
                    get-widget-property:hypereval-input
                      innerValue
                    hyper2lambda:x:/@get-widget-property/*/*?value
                    set:x:/@.exe/#/*
                    add:x:/@.exe/#
                      src:x:/@hyper2lambda/*
                    eval-mutable:x:/@.exe/#

                    /*
                     * Then converting lambda result to Hyperlambda, and returning back to client as JSON
                     */
                    lambda2hyper:x:/@.exe/#/*
                    get-widget-property:hypereval-output-wrapper
                      visible
                    if:x:/@get-widget-property/*/*?value
                      p5.web.return-response-object:code
                        src:x:/@lambda2hyper?value

                    /*
                     * Storing code in session, to make it more easily available.
                     */
                    p5.web.session.set:hypereval.code
                      src:x:/../*/get-widget-property/[0,1]/*/*?value

                /*
                 * Preview button.
                 */
                button:hypereval-preview-snippet
                  innerValue:@"<span class=""icon-play3""></span>"
                  title:Previews your page - Alt+P
                  disabled
                  oninit

                    /*
                     * Initially posting the "snippet changed" event, to make sure
                     * we can preview snippet, if possible.
                     */
                    hypereval.snippet.changed

                  events

                    /*
                     * Invoked when "current snippet" has been changed, either loaded
                     * from database, or saved to database.
                     */
                    hypereval.snippet.changed

                      /*
                       * Checking if this is a page, and if so, enabling button.
                       */
                      p5.web.session.get:hypereval.snippet-name
                      p5.mysql.connect:[hypereval]
                        p5.mysql.scalar:"select type from snippets where name = @name"
                          @name:x:/@p5.web.session.get/*?value
                        if:x:/@p5.mysql.scalar?value
                          =:page
                          delete-widget-property:x:/../*/_event?value
                            disabled
                        else
                          set-widget-property:x:/../*/_event?value
                            disabled

                  onclick

                    /*
                     * Previewing "current snippet" in a new window.
                     */
                    p5.web.session.get:hypereval.snippet-name
                    p5.mysql.connect:[hypereval]
                      p5.mysql.scalar:"select name from snippets where name = @name and type = 'page'"
                        @name:x:/@p5.web.session.get/*?value
                      if:x:/-?value
                        not
                        return
                      p5.html.url-encode:x:/@p5.mysql.scalar?value
                      p5.web.send-javascript:@"window.open('/hypereval/{0}');"
                        :x:/@p5.html.url-encode?value

                /*
                 * View "Output" button.
                 */
                button:hypereval-view-output
                  innerValue:@"<span class=""icon-eye""></span>"
                  title:View the output code, which results from evaluating your code - Alt+V
                  onclick

                    /*
                     * Toggles visibility of output.
                     */
                    get-widget-property:hypereval-output-wrapper
                      visible
                    if:x:/@get-widget-property/*/*?value

                      /*
                       * Hiding "output" widget.
                       */
                      set-widget-property:hypereval-output-wrapper
                        visible:false
                      micro.css.delete:x:/../*/_event?value
                        class:toggled

                    else

                      /*
                       * Showing "output" widget.
                       */
                      set-widget-property:hypereval-output-wrapper
                        visible:true
                      micro.css.add:x:/../*/_event?value
                        class:toggled

            div
              class:strip
              style:"display:inline-block;margin-left:1rem;"
              widgets

                /*
                 * New snippet button.
                 */
                button:hypereval-new-snippet
                  innerValue:@"<span class=""icon-plus""></span>"
                  title:New snippet - Alt+N
                  onclick

                    /*
                     * Asking user to confirm action, since this will empty his current code.
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-confirm-new-snippet-modal
                        widgets
                          h3
                            innerValue:Confirm action
                          p
                            innerValue:Are you sure you want to create a new snippet. This will empty your current Hyperlambda editor for its existing code.
                          div
                            class:right
                            widgets
                              div
                                class:strip
                                style:"display:inline-block;"
                                widgets
                                  button:hypereval-confirm-new-snippet
                                    innerValue:Yes
                                    style:"margin-bottom:0;"
                                    onclick:@"p5.$('hypereval-confirm-new-snippet').raise('.onclick',{onsuccess:function(ret, evt){p5.hypereval_input.getDoc().setValue(ret.code);}});return false;"
                                    oninit

                                      /*
                                       * Setting initial focus to "Yes" button.
                                       */
                                      micro.page.set-focus:x:/../*/_event?value

                                    .onclick

                                      /*
                                       * Clearing all relevant session variables, deleting modal widget, and
                                       * making sure we disable our "delete snippet button" and "preview snippet" button.
                                       */
                                      set-widget-property:hypereval-delete-snippet
                                        disabled
                                      set-widget-property:hypereval-preview-snippet
                                        disabled
                                      p5.web.session.set:hypereval.snippet-name
                                      p5.web.session.set:hypereval.code
                                      delete-widget:hypereval-confirm-new-snippet-modal
                                      p5.web.return-response-object:code
                                        src:@"/*
 * Your code goes here ...
 */
"

                                  button
                                    innerValue:No
                                    style:"margin-bottom:0;"
                                    onclick

                                      /*
                                       * Simply deleting modal widget.
                                       */
                                      delete-widget:hypereval-confirm-new-snippet-modal

                /*
                 * Load snippet button.
                 */
                button:hypereval-load-snippet
                  innerValue:@"<span class=""icon-folder-open""></span>"
                  title:Load snippet - Alt+L
                  events

                    /*
                     * Helper event to programatically open load snippet window.
                     */
                    hypereval.widgets.eval.show-load-window

                      /*
                       * To make it simple, we simply "click" button programmatically.
                       */
                      p5.web.widgets.ajax-events.raise:x:/../*/_event?value
                        onclick

                    /*
                     * Helper event to programatically close load snippet window.
                     */
                    hypereval.widgets.eval.close-load-window

                      /*
                       * Closing modal window, if it exists.
                       */
                      if
                        fetch:x:/0/0?value
                          widget-exists:hypereval-manage-snippets
                        delete-widget:hypereval-manage-snippets

                  onclick

                    /*
                     * Shows a modal widget with each snippet from database, allowing
                     * the user to either mark it as a "startup snippet" or as a "page".
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-manage-snippets
                        widgets
                          h3
                            innerValue:Your snippets
                          micro.widgets.grid:hypereval-snippets-grid
                            class:hover striped
                            events

                              /*
                               * Databinds snippets grid.
                               */
                              hypereval.snippets.databind

                                /*
                                 * Connecting to database, selecting all snippets, 
                                 * and creating one grid item for each record.
                                 */
                                p5.mysql.connect:[hypereval]
                                  p5.mysql.select:@"select * from snippets order by id desc"

                                  /*
                                   * Necessary to find the relative URL of module.
                                   */
                                  p5.io.unroll-path:@HYPEREVAL/
                                  split:x:/@p5.io.unroll-path?value
                                    =:/

                                  /*
                                   * Looping through each snippet, creating one datagrid item
                                   * for each.
                                   */
                                  for-each:x:/@p5.mysql.select/*
                                    p5.html.url-encode:x:/@_dp/#/*/name?value

                                    /*
                                     * Adding "name" cell.
                                     */
                                    if:x:/@_dp/#/*/type?value

                                      /*
                                       * Snippet has a type declaration (either a "page" or a "startup" type of snippet).
                                       */
                                      eval-x:x:/+/*/*/*
                                      add:x:/../*/micro.widgets.grid.databind
                                        src
                                          item
                                            name:{0} ({1})
                                              :x:/@_dp/#/*/name?value
                                              :x:/@_dp/#/*/type?value

                                    else

                                      /*
                                       * Plain snippet.
                                       */
                                      eval-x:x:/+/*/*/*
                                      add:x:/../*/micro.widgets.grid.databind
                                        src
                                          item
                                            name:x:/@_dp/#/*/name?value

                                    /*
                                     * Adding commonalities for row.
                                     */
                                    eval-x:x:/+/**/.name
                                    add:x:/../*/micro.widgets.grid.databind/0/-
                                      src
                                        .row
                                          onclick:@"p5.$(event.target.parentNode.id).raise('.onclick',{onsuccess:function(ret, evt){if (ret.code){p5.hypereval_input.getDoc().setValue(ret.code);}}});return false;"
                                          .onclick

                                            /*
                                             * Forward evaluated above.
                                             */
                                            .name:x:/@_dp/#/*/name?value
                                            p5.mysql.connect:[hypereval]
                                              p5.mysql.select:@"select content, name from snippets where name = @name"
                                                @name:x:/@.name?value
                                              p5.web.return-response-object:code
                                                src:x:/@p5.mysql.select/*/*/content?value
                                              p5.web.session.set:hypereval.snippet-name
                                                src:x:/@p5.mysql.select/*/*/name?value

                                            /*
                                             * Closing modal window.
                                             */
                                            delete-widget:hypereval-manage-snippets

                                            /*
                                             * Making sure our Hypereval remembers that this is our
                                             * "current code".
                                             */
                                            p5.web.session.set:hypereval.code
                                              src:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/content?value

                                            /*
                                             * Signaling that current snippet was changed.
                                             */
                                            hypereval.snippet.changed

                                            /*
                                             * Enabling delete snippet button.
                                             */
                                            delete-widget-property:hypereval-delete-snippet
                                              disabled

                                        name:x:/@_dp/#/*/name?value


                                /*
                                 * Databinds snippets.
                                 */
                                micro.widgets.grid.databind:hypereval-snippets-grid

                            oninit

                              /*
                               * Initial databinding of snippets grid.
                               */
                              hypereval.snippets.databind
                          div
                            class:right
                            widgets
                              div
                                class:strip
                                style:"display:inline-block;"
                                widgets
                                  button
                                    innerValue:@"<span class=""icon-cross""></span>"
                                    style:"margin-bottom:0;"
                                    title:Close window without loading  snippet
                                    onclick

                                      /*
                                       * Simply deletes the modal widget.
                                       */
                                      delete-widget:hypereval-manage-snippets

                /*
                 * Save snippet button.
                 */
                button:hypereval-save-snippet
                  innerValue:@"<span class=""icon-floppy-disk""></span>"
                  title:Saves snippet - Alt+S
                  onclick

                    /*
                     * Checking if we should provide a default alternative value for "type" of snippet
                     * radio buttons.
                     */
                    p5.web.session.get:hypereval.snippet-name
                    if:x:/@p5.web.session.get/*?value
                      p5.mysql.connect:[hypereval]
                        p5.mysql.scalar:@"select type from snippets where name = @name"
                          @name:x:/@p5.web.session.get/*?value
                        if:x:/@p5.mysql.scalar?value
                          add:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/={0}
                            :x:/@p5.mysql.scalar?value
                            src:checked

                    /*
                     * Making sure we check of "Snippet" radio button, if no other radio buttons have been checked
                     * by above logic.
                     */
                    if:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/checked
                      not
                      add:x:/../*/create-widgets/**/micro.widgets.wizard-form/*/radio-group/*/options/*/=snippet
                        src:checked

                    /*
                     * Saving Hyperlambda to database, by allowing user to associate a name with snippet,
                     * through a modal window, for then to save the code with the specified name.
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-save-modal
                        widgets
                          h3
                            innerValue:Supply a name please
                          p
                            innerValue:And choose if you want to save your snippet as a page or a startup snippet. Notice, if you supply a name of a snippet that already exists, the old snippet will be overwritten.
                          micro.widgets.wizard-form:hypereval-save-form
                            text
                              info:Name
                              .data-field:name
                              onkeydown:@"if (event.keyCode == 13) {p5.$('hypereval-save-modal-button').raise('onclick');return false;}"
                              oninit

                                /*
                                 * Setting initial focus to "Name" textbox.
                                 */
                                micro.page.set-focus:x:/../*/_event?value

                                /*
                                 * Checking if we have a snippet name in session, and
                                 * if so, we make sure we default our name to that value.
                                 */
                                p5.web.session.get:hypereval.snippet-name
                                if:x:/-/*?value
                                  set-widget-property:x:/../*/_event?value
                                    value:x:/@p5.web.session.get/*?value

                            radio-group
                              .data-field:type
                              options
                                Snippet:snippet
                                Page:page
                                Startup:startup

                            div
                              class:right
                              style:"margin-top:-1rem;"
                              widgets
                                div
                                  class:strip
                                  style:"display:inline-block;"
                                  widgets
                                    button:hypereval-save-modal-button
                                      innerValue:@"<span class=""icon-checkmark""></span>"
                                      title:Saves your snippet
                                      style:"margin-bottom:0;padding-left:35px;padding-right:35px;"
                                      onclick

                                        /*
                                         * Verfying there actually is any code.
                                         */
                                        get-widget-property:hypereval-input
                                          innerValue
                                        if
                                          trim:x:/@get-widget-property/*/*?value
                                          =:

                                          /*
                                           * No code.
                                           */
                                          micro.windows.info:There is no code to save
                                            class:micro-windows-info warning
                                          return

                                        /*
                                         * Serializing form, opening database connection,
                                         * deleting snippets with similar name, and saving snippet
                                         * into database.
                                         */
                                        micro.widgets.wizard-form.value:hypereval-save-form

                                        /*
                                         * Sanity checking name of snippet.
                                         */
                                        match:x:/@micro.widgets.wizard-form.value/*/name?value
                                          src:regex:/^[-a-z0-9_ ]*$/i
                                        if:x:/@match/*?name
                                          not
                                          micro.windows.info:Only use spaces, a-z, 0-9 and '-' in your name
                                            class:micro-windows-info warning
                                          return

                                        /*
                                         * Making sure we insert page as correct type.
                                         */
                                        _type
                                        if:x:/@micro.widgets.wizard-form.value/*/type?value
                                          !=:snippet
                                          set:x:/@_type?value
                                            src:x:/@micro.widgets.wizard-form.value/*/type?value

                                        /*
                                         * Inserting snippet into database.
                                         */
                                        p5.mysql.connect:[hypereval]
                                          p5.mysql.delete:@"delete from snippets where name = @name"
                                            @name:x:/@micro.widgets.wizard-form.value/*/name?value
                                          p5.mysql.insert:@"insert into snippets (name, content, type) values (@name, @content, @type)"
                                            @name:x:/@micro.widgets.wizard-form.value/*/name?value
                                            @content:x:/@get-widget-property/*/*?value
                                            @type::x:/@_type?value

                                        /*
                                         * Closing modal window, and giving user some feedback.
                                         */
                                        delete-widget:hypereval-save-modal
                                        micro.windows.info:Snippet was successfully saved
                                          class:micro-windows-info success

                                        /*
                                         * Making sure we store name used into session, in case
                                         * user wants to save the same snippet again.
                                         */
                                        p5.web.session.set:hypereval.snippet-name
                                          src:x:/@micro.widgets.wizard-form.value/*/name?value

                                        /*
                                         * Storing code in session, to make it more easily available.
                                         */
                                        p5.web.session.set:hypereval.code
                                          src:x:/../*/get-widget-property/*/*?value

                                        /*
                                         * Signaling that snippet was changed.
                                         */
                                        hypereval.snippet.changed

                                        /*
                                         * Making sure we disable delete snippet button, 
                                         * since there are no active snippets now.
                                         */
                                        delete-widget-property:hypereval-delete-snippet
                                          disabled

                                    button
                                      innerValue:@"<span class=""icon-cross""></span>"
                                      title:Closes your save window without saving
                                      style:"margin-bottom:0;"
                                      onclick

                                        /*
                                         * Simply closing modal window.
                                         */
                                        delete-widget:hypereval-save-modal

                /*
                 * Delete snippet button.
                 */
                button:hypereval-delete-snippet
                  innerValue:@"<span class=""icon-bin""></span>"
                  title:Deletes snippet - Alt+D
                  disabled
                  oninit

                    /*
                     * Checking if we have a "current snippet", and if so, enabling our delete snippet button.
                     */
                    p5.web.session.get:hypereval.snippet-name
                    if:x:/-/*?value

                      /*
                       * There's a "current snippet" loaded in our editor.
                       */
                      delete-widget-property:hypereval-delete-snippet
                        disabled

                  onclick

                    /*
                     * Asks user to confirm action.
                     */
                    create-widgets
                      micro.widgets.modal:hypereval-confirm-snippet-deletion
                        widgets
                          h3
                            innerValue:Confirm action
                          p
                            innerValue:Please confirm deletion of snippet. This action is permanent.
                          div
                            class:right
                            widgets
                              div
                                class:strip
                                style:"display:inline-block;"
                                widgets
                                  button:hypereval-confirm-deletion-btn
                                    innerValue:@"<span class=""icon-checkmark""></span>"
                                    style:"margin-bottom:0;padding-left:35px;padding-right:35px;"
                                    title:Deletes snippet
                                    oninit

                                      /*
                                       * Setting initial focus to "Yes" button.
                                       */
                                      micro.page.set-focus:x:/../*/_event?value

                                    onclick:@"p5.$('hypereval-confirm-deletion-btn').raise('.onclick',{onsuccess:function(ret, evt){p5.hypereval_input.getDoc().setValue('');}});return false;"
                                    .onclick

                                      /*
                                       * Figuring out which snippet user wants to delete, if any.
                                       */
                                      p5.web.session.get:hypereval.snippet-name
                                      if:x:/@p5.web.session.get/*?value
                                        not
                                        or:x:/@p5.web.session.get/*?value
                                          =:

                                        /*
                                         * Snippet is probably not saved yet.
                                         */
                                        return

                                      /*
                                       * Opening database, and deleting snippet from snippet collection.
                                       */
                                      p5.mysql.connect:[hypereval]
                                        p5.mysql.delete:@"delete from snippets where name = @name"
                                          @name:x:/@p5.web.session.get/*?value

                                      /*
                                       * Cleaning out session variables.
                                       */
                                      p5.web.session.set:hypereval.snippet-name
                                      p5.web.session.set:hypereval.code
                                      p5.web.session.set:hypereval.file-type

                                      /*
                                       * Making sure we disable delete snippet button, 
                                       * since there are no active snippets now.
                                       */
                                      set-widget-property:hypereval-delete-snippet
                                        disabled

                                      /*
                                       * Deleting modal window.
                                       */
                                      delete-widget:hypereval-confirm-snippet-deletion

                                      /*
                                       * Disabling preview pagebutton.
                                       */
                                      set-widget-property:hypereval-preview-snippet
                                        disabled

                                  button
                                    innerValue:@"<span class=""icon-cross""></span>"
                                    title:Close window without deleting snippet
                                    style:"margin-bottom:0;"
                                    onclick

                                      /*
                                       * Simply deleting modal window.
                                       */
                                      delete-widget:hypereval-confirm-snippet-deletion

        div:hypereval-output-wrapper
          visible:false
          style:"clear:both;"
          widgets
            literal:hypereval-output
              element:textarea
              class:fill
              rows:10
              oninit

                /*
                 * Transforming output textarea to CodeMirror instance.
                 */
                p5.io.unroll-path:@HYPEREVAL/
                p5.web.send-javascript:@"p5.hypereval_output = CodeMirror.fromTextArea(p5.$('hypereval-output').el, {{
mode:'hyperlambda',
theme:'hyperlambda',
lineNumbers:true,
styleActiveLine:true,
path:'{0}/media/codemirror/',
tabSize:2,
indentAuto:true,
readOnly:true
}});"
                  :x:/@p5.io.unroll-path?value
