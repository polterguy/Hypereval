/*
 * Creates a lambda Web Service endpoint. __Notice__, this is a simple web service endpoint
 * example, illustrating using a __[eval-whitelist]__ invocation to (securely) evaluate a
 * lambda web service, where the client supplies the Hyperlambda to evaluate on the web server's
 * endpoint.
 *
 * This particular web service endpoint doesn't really allow the client to do much, since only
 * basic Active Events have been whitelisted - However, it serves as a template and example for
 * you to create your own lambda web services.
 *
 * Combined with the _"invoke-lambda-ws.page.hl"_ snippet though, it allows you to play around
 * with lambda web services, by using _"localhost"_ as your web service endpoint, to get a feel for
 * how a lambda web service might be implemented. This particular web service endpoint only enables
 * the following Active Events.
 *
 * - __[set]__
 * - __[add]__
 * - __[insert-before]__
 * - __[insert-after]__
 * - __[src]__
 * - __[+]__
 * - __[-]__
 * - __[/]__
 * - __[*]__
 * - __[for-each]__
 * - __[return]__
 */


/*
 * Sanity checking invocation.
 */
if
  p5.web.request.get-method
  !=:POST
  p5.web.response.set-status-code:405
  p5.web.response.set-status-description:405 Only POST method is allowed


/*
 * De-serialising request.
 */
p5.web.request.parse-mime


/*
 * Evaluating specified Hyperlambda in a "whitelist" invocation to [eval].
 *
 * Notice, whatever keywords you wish to allow your clients to be legally allowed to evaluate,
 * must be declared in the [events] lambda below.
 */
hyper2lambda:x:/@p5.web.request.parse-mime/**/content?value
eval-whitelist:x:/@hyper2lambda
  events
    set
    add
    insert-before
    insert-after
    src
    +
    -
    /
    *
    for-each
    return


/*
 * Yielding results of above evaluation back to client.
 */
p5.web.echo:x:/@eval-whitelist
